ISREDIT MACRO (NLINES)
/*
/* UNX EDIT MACRO - SHOW FIRST N LINES OF EACH 'X-CLUDED' BLOCK
/*                  IN THE MEMBER
/*                  DEFAULT = 1 LINE
/*
CONTROL NOFLUSH NOMSG NOLIST NOCONLIST NOSYMLIST
IF &NLINES = HELP OR -
   &NLINES = ?  THEN -
  DO
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(UNX#)
    EXIT
  END
IF &NLINES = &STR() THEN -
  SET &NLINES = 1
ISPEXEC CONTROL ERRORS RETURN
SET &RC  = 0
SET &LINE = 1
DO WHILE &RC < 12
  ISREDIT (XSTATUS) = XSTATUS &LINE
  SET &RC = &LASTCC
  IF &RC = 12 THEN -
    EXIT
  IF &XSTATUS = &STR(X) THEN -
    DO
      SET &XLINES = 0
      DO WHILE &XLINES < &NLINES
        ISREDIT (XSTATUS) = XSTATUS &EVAL(&LINE+&XLINES)
        SET &RC = &LASTCC
        IF &RC = 12 THEN -
          EXIT
        ISREDIT XSTATUS &EVAL(&LINE+&XLINES) = NX
        SET &RC = &LASTCC
        IF &RC = 12 THEN -
          EXIT
        SET &XLINES = &XLINES + 1
      END
      SET &LINE = &LINE + &EVAL(&NLINES)
      DO WHILE &XSTATUS = &STR(X)
        ISREDIT (XSTATUS) = XSTATUS &LINE
        SET &RC = &LASTCC
        IF &RC = 12 THEN -
          EXIT
        SET &LINE = &LINE + 1
      END
    END
  ELSE -
    SET &LINE = &LINE + 1
END
ISREDIT LOCATE 0
EXIT CODE(1)  /* RETURN CURSOR TO COMMAND LINE */
END
/* J. KALINICH */
/* EDIT MACRO TO SHOW FIRST N LINES IN EACH 'X-CLUDED' BLOCK    */
