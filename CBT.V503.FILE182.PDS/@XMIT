         TITLE 'P D S  --  PDS XMIT COMMAND INTERFACE        04/04/06'
***********************************************************************
***      XMIT SUBCOMMAND      ADDED BY JOHN KALINICH -- APR., 2006  ***
***********************************************************************
*
         SPACE 1
XMIT     CSECT
         USING *,R8
XMI2100  MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    XMI2200               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    XMI2110               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    XMI2200               YES, BRANCH
XMI2110  TM    #XMIALI,#XMIASS2      ASSOCIATES TOO?
         BO    XMI2200               YES, BRANCH
         SPACE 1
XMI2120  DS    0H
         TM    #XMIALI,#XMIASS2+2    ALIAS/NOALIAS SPECIFIED?
         BM    XMI2130               YES
*        OI    #XMIALI,&XMITALI      USE THE DEFAULT
         OI    #XMIALI,1             USE THE DEFAULT          DRK APR06
         TM    #XMIALI,#XMIASS2      ASSOCIATES TOO?
         BO    XMI2200               YES
         SPACE 3
XMI2130  DS    0H
         OI    #XMIALI,#XMIONE       ONLY A SINGLE MEMBER/DATA SET
         LA    R4,DDNAMEH            HOLD FOR THIS SUBCOMMAND
         MVC   0(8,R4),DIRNAME
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         B     XMI2900
XMI2200  OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         TM    #XMIALI,#XMIASS2+2    ALIAS/NOALIAS SPECIFIED?
         BM    *+8                   YES
*        OI    #XMIALI,&XMITALI      NO, USE THE DEFAULT
         OI    #XMIALI,1             NO, USE THE DEFAULT      DRK APR06
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         SPACE 1
XMI2210  SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   XMI2220               YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     XMI2500
         SPACE 1
XMI2220  SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
XMI2300  TM    #XMIALI,#XMIASSP      ASSOCIATES PHASE?
         BO    XMI2320               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    XMI2320               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   XMI2500               NO, BRANCH
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    XMI2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BNO   XMI2605               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   XMI2605               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    XMI2319               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1       MOVE TO BLDLLIST
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     XMI2315                   00 - SUCCESSFUL
         B     XMI2310                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
XMI2310  MVC   INSERT#1(8),DIRNAME
         M$MSG L853$1                MSG - (MEMBER) NOT FOUND
         B     XMI2300
         SPACE 1
XMI2315  MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     XMI2500
         SPACE 1
XMI2319  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
XMI2320  BAS   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     XMI2600               END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #XMIALI,#XMIASSP      ASSOCIATES PHASE?
         BO    XMI2400               YES, BRANCH
         SPACE 1
         BAS   R2,MFILTER            CHECK MEMBER NAMES
         B     XMI2600                 00 - SIMULATE DIRECTORY END
         B     XMI2300                 04 - MEMBER FAILED FILTERING
         B     XMI2500                 08 - MEMBER PASSED FILTERING
         SPACE 3
XMI2400  L     R1,#MEMPTR            START OF MEMBER LIST
         B     XMI2410+4             SKIP FIRST ADD
XMI2410  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   XMI2300               YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   XMI2410               NO, BRANCH
         SPACE 3
XMI2500  L     R1,#MEMPTR            START OF MEMBER LIST
         B     XMI2510+4             SKIP FIRST ADD
XMI2510  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   XMI2580               YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    XMI2300               YES, IGNORE THIS ENTRY
         B     XMI2510               NO, CONTINUE SEARCHING
         SPACE 2
XMI2580  MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   XMI2210               NO, BRANCH
         B     XMI2300               YES, GET NEXT MEMBER
         SPACE 3
XMI2600  TM    #XMIALI,#XMIASSP      ASSOCIATED MEMBERS?
         BO    XMI2605               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    XMI2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    XMI2300               YES, BRANCH
         SPACE 1
XMI2605  TM    #XMIALI,#XMIASSP      ASSOCIATES PHASE STARTED?
         BO    XMI2700               YES, BRANCH
         TM    #XMIALI,#XMIASS2      ASSOCIATE MEMBERS TOO?
         BNO   XMI2700               NO, BRANCH
         OI    #XMIALI,#XMIASSP      YES, ASSOCIATES PHASE STARTED
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     XMI2300               DO ANY ASSOCIATED MEMBERS
         SPACE 3
XMI2700  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         SPACE 3
XMI2900  DS    0H
         ST    R4,#MEMLAST           SAVE LAST MEMBER POINTER
         SPACE 1
         L     R0,=A(32*1024)           LENGTH OF BUFFER
         ICM   R0,B'1000',=AL1(26)      SUBPOOL 26
         GETMAIN R,LV=(0)               GET XMIT BUFFER
         ST    R1,XMIBUFFP              XMIT COMMAND BUFFER
         LR    R5,R1                    BUFFER START
         MVC   0(5,R5),XMITCMD          XMIT COMMAND
         LA    R5,5(,R5)
         MVC   0(20,R5),#XMIADD         ADDRESSEE LIST
         LA    R5,20(,R5)
         MVC   0(5,R5),DSNL             DSN('
         MVC   5(44,R5),DSNAME          CURRENT DATASET
         LH    R15,DSNLEN               DSN LENGTH
         LA    R5,5(R15,R5)
         MVC   0(3,R5),DSNR                 ')
         LA    R5,3(,R5)
XMI2910  DS    0H
         CLI   #XMIDSN,C' '             OUTPUT DSN SPECIFIED?
         BE    XMI2920                  NO, BRANCH
         MVC   0(7,R5),OUTDSN           YES
         MVC   7(46,R5),#XMIDSN         OUTDSN
         LH    R15,#XMIDSNL             OUTDSN LENGTH
         LA    R5,7(R15,R5)
         MVC   0(2,R5),=CL2') '
         LA    R5,2(,R5)
         B     XMI3000
XMI2920  DS    0H
         CLI   #XMIDDN,C' '             OUTPUT DDNAME SPECIFIED?
         BE    XMI3000                  NO, BRANCH
         MVC   0(7,R5),OUTDDN           YES
         MVC   7(8,R5),#XMIDDN          OUTDDN
         LH    R15,=H'8'                OUTDDN LENGTH
         LA    R5,7(R15,R5)
         MVC   0(2,R5),=CL2') '
         LA    R5,2(,R5)
XMI3000  DS    0H
         CLI   #XMILOG,2                NOLOG?
         BNE   XMI3005                  NO, BRANCH
         MVC   0(6,R5),NOLOG
         LA    R5,6(,R5)
XMI3005  DS    0H                                             DRK APR09
         CLI   #XMIWARN,2               NOWARN?               DRK APR09
         BNE   XMI3007                  NO, BRANCH            DRK APR09
         MVC   0(7,R5),NOWARN                                 DRK APR09
         LA    R5,7(,R5)                                      DRK APR09
XMI3007  DS    0H                                             DRK AUG19
         CLI   #XMIMSG,1                MESSAGE?              DRK AUG19
         BNE   XMI3010                  NO, BRANCH            DRK AUG19
         MVC   0(8,R5),XMESSAGE                               DRK AUG19
         LA    R5,8(,R5)                                      DRK AUG19
XMI3010  DS    0H
         CLI   #XMINOT,2                NONOTIFY?
         BNE   XMI3020                  NO, BRANCH
         MVC   0(9,R5),NONOTIFY
         LA    R5,9(,R5)
XMI3020  DS    0H
         CLI   #XMIOUT,C' '             SYSOUT SPECIFIED?
         BE    XMI3030                  NO, BRANCH
         MVC   0(10,R5),SYSOUTC
         MVC   7(1,R5),#XMIOUT          SYSOUT CLASS
         LA    R5,10(,R5)
XMI3030  DS    0H
         SPACE 1
         TM    FLAGSHH,FMEMALLM         ALL MEMBERS?
         BNO   XMI6000                  NO
         BCTR  R5,0                     YES, PT TO END
         B     XMI6585
XMI6000  DS    0H
         MVC   0(8,R5),MEMBERSL
         LA    R5,8(,R5)
         SPACE 1
XMI6010  DS    0H
         L     R3,#MEMPTR                 FIRST MEMBER POINTER
         L     R4,#MEMLAST                LAST MEMBER POINTER
         SPACE 1
         LA    R2,3620                    MAX NUMBER OF MEMBERS
XMI6530  CR    R3,R4                      END OF MEMBER TABLE?
         BNL   XMI6580                    YES
         MVC   0(8,R5),0(R3)              MEMBER NAME
         MVI   8(R5),C' '                 SPACE
         LA    R3,16(,R3)                 NEXT MEMBER POSITION
         LA    R5,9(,R5)                  NEXT MEMBER SLOT
         BCT   R2,XMI6530                 LOOP
         M$MSG L533                       MAX NUMBER OF MEMBERS MSG
         SPACE 1
XMI6580  DS    0H
         MVI   0(R5),C')'                 END OF MEMBERS(...
XMI6585  DS    0H
         L     R3,XMIBUFFP                XMIT COMMAND BUFFER
         LA    R5,1(,R5)                  ADD ONE FOR LENGTH CALC
         SR    R5,R3                      COMMAND BUFFER LENGTH
         OI    ##ADRPA#,$A                DEFER ALL ATTENTIONS
         STAX  XMITNULL,USADDR=(R7),REPLACE=NO,IBUF=0,                 X
               OBUF=(XMI484W,XMI484WL),MF=(E,STAXPARM)
         XC    #SUBHOLD(28+20),#SUBHOLD
         LA    R14,#SUBHOLD+28            POINT TO FLAGS
         LR    R15,R3                     POINT TO COMMAND TEXT
         LA    R0,#SUBHOLD+28+4           POINT TO COMMAND TEXT LEN
         LA    R1,#SUBHOLD+28+8           POINT TO RETURN CODE
         LA    R2,#SUBHOLD+28+12          POINT TO REASON CODE
         LA    R3,#SUBHOLD+28+16          POINT TO ABEND CODE
         STM   R14,R3,#SUBHOLD            UPDATE IKJEFTSR PARM LIST
         OI    #SUBHOLD+20,X'80'          INDICATE THE END OF THE LIST
         MVI   #SUBHOLD+28+3,X'05'        A COMMAND IS BEING INVOKED
         ST    R5,#SUBHOLD+28+4           COMMAND BUFFER LENGTH
         SPACE 2
         L     R2,RECOVER
         LA    R1,XMI7080                 RECOVERY ADDRESS
         ST    R1,RECOVER
         STM   R2,R8,#SUBHOLD+88          SAVE REGISTERS
         SPACE 1
         LA    R3,##SUBCAL                WHO TO ATTACH
         LA    R1,#CMXLIST                PARAMETERS TO PASS
         CLC   ##SUBCAL(8),XMIEFTSR       INVOKE IKJEFTSR?
         BNE   XMINLNK                    NO, BRANCH
         LA    R1,#SUBHOLD                POINT TO THE PARAMETER LIST
         SPACE 2
XMINLNK  BAS   R2,ATTACH                  XMIT
         SPACE 3
XMI7080  LM    R2,R8,#SUBHOLD+88
         ST    R2,RECOVER                 RESET RECOVERY ADDRESS
         STAX  ,
         NI    ##ADRPA#,FF-$A             ALLOW ATTENTIONS AGAIN
         MVI   ATTNECB,0                  CLEAR THE ATTENTION ECB
         MVC   SUBSECB+3(1),#SUBHOLD+28+11  COPY FINAL RETURN CODE
         SPACE 1
XMI7500  DS    0H
         CLC   ##SUBCAL(8),XMIEFTSR       INVOKE IKJEFTSR?
         BNE   XMI8550                    NO, BRANCH
         CLI   #SUBHOLD+28+11,8           RC:1                DRK JUL11
         BL    XMI8550                    LOW, VALID XMIT
         MVI   MTLEN+4,11
         MVC   INSERT#2(11),MSGBL132      CLEAR AREA
         SR    R15,R15
         ICM   R15,B'0001',#SUBHOLD+28+11                     DRK JAN07
         CVD   R15,DOUBLE
         MVC   INSERT#1(8),##SUBCOM
         MVC   INSERT#2(4),=X'40202120'
         ED    INSERT#2(4),DOUBLE+6
         MVI   INSERT#2,C'='              RETURN CODE
         SPACE 1
         CLI   SUBSECB+3,X'0C'            IKJEFTSR RC=12 (PGM ABEND)?
         BNE   XMI8530                    NO, SKIP MESSAGE
         SPACE 1
         L     R0,#SUBHOLD+28+16        ABEND CODE            DRK NOV07
         SRDL  R0,12                    SHIFT USER ABEND TO R1DRK NOV07
         ST    R0,FULLWORD              SYSTEM ABEND CODE     DRK NOV07
         MVC   INSERT#2+4(7),=C' ABEND='                      DRK NOV07
         UNPK  INSERT#2+11(5),FULLWORD+2(3)                   DRK NOV07
         TR    INSERT#2+11(4),TRTABLE                         DRK NOV07
         MVI   INSERT#2+11,C'S'         S000                  DRK NOV07
         MVI   INSERT#2+11+4,X'40'                            DRK NOV07
         SPACE 1                                              DRK NOV07
         SRL   R1,20                    USER ABEND            DRK NOV07
         CVD   R1,DOUBLE                                      DRK NOV07
         UNPK  DOUBLE(5),DOUBLE+5(3)                          DRK NOV07
         OI    DOUBLE+4,X'F0'           MAKE PRINTABLE        DRK NOV07
         MVI   INSERT#2+11+5,C'U'       U0000                 DRK NOV07
         MVC   INSERT#2+11+6(4),DOUBLE+1 GET USER ABEND       DRK NOV07
         SPACE 1
XMI8530  DS    0H
         M$MSG L171$2
XMI8550  DS    0H
         L     R1,XMIBUFFP              XMIT COMMAND BUFFER
         L     R0,=A(32*1024)           LENGTH OF BUFFER
         ICM   R0,B'1000',=AL1(26)      SUBPOOL 26
         FREEMAIN R,LV=(0),A=(1)        FREE XMIT BUFFER
         B     NEWSTAX
XMITNULL BR    R14
         SPACE 2
*  XMIT CONSTANTS
         SPACE 2
XMIEFTSR DC    CL8'IKJEFTSR'
         SPACE 2
XMI484W  DC    C'PDS484W XMIT SHOULD NOT BE INTERRUPTED'
XMI484WL EQU   *-XMI484W
         SPACE 2
XMITCMD  DC    CL5'XMIT '
DSNL     DC    CL5'DSN('''
DSNR     DC    CL3''') '
OUTDSN   DC    CL7'OUTDSN('
OUTDDN   DC    CL7'OUTDDN('
NOLOG    DC    CL6'NOLOG '
NOWARN   DC    CL7'NOWARN '                                   DRK APR09
XMESSAGE DC    CL8'MESSAGE '                                  DRK AUG19
NONOTIFY DC    CL9'NONOTIFY '
SYSOUTC  DC    CL10'SYSOUT( ) '
MEMBERSL DC    CL8'MEMBERS('
         SPACE 2
#XMIASSP EQU   X'80'            XMIT: ASSOCIATES PHASE
#XMIONE  EQU   X'10'            XMIT: A SINGLE MEMBER
#XMIASS2 EQU   X'01'            XMIT: XMIT ASSOCIATES TOO
