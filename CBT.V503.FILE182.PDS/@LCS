         TITLE 'P D S  --  PDS LCS                           03/28/06'
***********************************************************************
***      LCS SUBCOMMAND   THE HARTFORD GROUP MOD                    ***
***********************************************************************
*
         SPACE 1
LCS      CSECT                                                THG MAR06
         USING *,R8
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    LCS0020             YES, BRANCH
         SPACE 1
         MVC   INSERT#1(72),SPFSTAT3
         MVI   MTLEN,72
         TM    FLAGSHH,FHEAD       HEADER WRITTEN YET?
         BO    LCS0000             YES, BRANCH
         M$MSG L230$1
         OI    FLAGSHH,FHEAD       NOW IT HAS BEEN WRITTEN
         SPACE 1
*
*        SOURCE MODULE PROCESSING
*
LCS0000  MVC   MSGTEXT1(136),MSGBL132  CLEAR THE MESSAGE AREA
         MVC   INSERT#1+1(8),DIRNAME
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   INSERT#1+1+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   LCS0010             NO, BRANCH
         TM    DIRSPFCR,X'FE'      0C OF 0CYYDDDF ZEROS?
         BNZ   LCS0010             NO, BRANCH
         TM    DIRSPFCD,X'FE'      0C OF OTHER 0CYYDDDF ZERO?
         BNZ   LCS0010             NO, BRANCH
         SPACE 1
         LA    R2,INSERT#1-7           POSITION FOR HEADER
         SR    R1,R1
         IC    R1,DIRSPFR              REVISION NUMBER
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV              VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAS   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         OI    27(R2),X'F0'            UNSUPPRESS ZERO (Y2K)
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAS   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         OI    37(R2),X'F0'            UNSUPPRESS ZERO (Y2K)
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         LH    R1,DIRSPFSI             CURRENT SIZE
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         SR    R1,R1                   CLEAR R1               DRK MAY10
         ICM   R1,B'0011',DIRSPFIN     INITIAL SIZE           DRK MAY10
*        LH    R1,DIRSPFIN             INITIAL SIZE
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         SR    R1,R1                   CLEAR R1               DRK MAY10
         ICM   R1,B'0011',DIRSPFMD     MODIFIED LINES         DRK MAY10
*        LH    R1,DIRSPFMD             MODIFIED LINES
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID       USERID
         B     LCS0015                 NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
LCS0010  DS    0H
         CLC   ZERO,0(R2)              ZERO?
         BE    LCS0015                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    LCS0015                 YES, NO SSI
         SPACE 1
         MVC   INSERT#1+8+5(4),=C'SSI:'
         UNPK  INSERT#1+8+5+5(9),0(5,R2)
         TR    INSERT#1+8+5+5(8),TRTABLE
         MVI   INSERT#1+8+5+5+8,X'40'
LCS0015  DS    0H
         MVI   MTLEN,72
         M$MSG L230$1
         B     NEWCMD
         SPACE 3
*
*        LOAD MODULE PROCESSING
*
LCS0020  OC    #ZAPOPT(4),#ZAPOPT      ANY ZAP, USER, TRAN OR LKED?
         BNZ   *+10                    YES, BRANCH
         OC    #ZAPOPT(4),=X'01010101' NO, DEFAULT TO DO THEM ALL
         SPACE 1
         L     R15,=A(READESD)     FORMAT THE ESD DATA
         BASR  R14,R15             ANY ESD DATA?
         NOP   0(,R0)              NO, CONTINUE ANYWAY
         SPACE 2
         L     R15,=A(READIDR)     FORMAT THE IDR DATA
         BASR  R14,R15             IS IDR DATA AVAILABLE?
         B     NOLCS               NO, MESSAGE AND QUIT
         B     LCS0USR$            YES, CONTINUE
         SPACE 1
NOLCS    SR    R6,R6
         B     LCS0USR2
         SPACE 3
LCS0HDR  TM    FLAGSHH,FDELAYM          ANY HEADER MESSAGE?
         BNO   LCS0HDR2                 NO, BRANCH
         M$ERRST MSGBLANK               OUTPUT A BLANK LINE
         M$ERRST DELAYMSG               OUTPUT THE HEADER LINE
         NI    FLAGSHH,FF-FDELAYM       TURN OFF THE HEADER LINE
LCS0HDR2 TM    FLAGSCC,F1IDR       FIRST IDR RECORD?
         BZR   R4                  NO, RETURN
         NI    FLAGSCC,FF-F1IDR
         LTR   R1,R6               ANY SECOND HEADER?
         BZR   R4                  NO, RETURN
         M$ERRST (R1)              INDIVIDUAL HISTORY TITLE
         BR    R4
         EJECT
*
*        FORMAT THE USER-SUPPLIED IDR DATA RECORDS
*        FROM THE PANVALET LIBRARY CONTROL SYSTEM (LCS)
*
         SPACE
         USING IDRENTRY,R3
         USING ESDENTRY,R2
LCS0USR$ LA    R3,#IDRPTR
         OI    FLAGSCC,F1IDR
         SPACE
LCS0USR  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    LCS0USR2            YES, BRANCH
         CLI   IDRTYPE,IDRUSER     USER IDR DATA RECORD?
         BNE   LCS0USR             NO, BRANCH
         SPACE
         LA    R2,#ESDPTR          ADDRESS OF ESD CHAIN
         SPACE
LCS0USR1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    LCS0USR             YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   LCS0USR1            NO, BRANCH
         CLC   ESDID,IDRESDID      WANTED ESD RECORD?
         BNE   LCS0USR1            NO, BRANCH
         CLC   ESDNAME,HIGDUMMY    IS IT THE DUMMY CSECT?
         BNE   LCS0USR1            NO, BRANCH
         CLC   IDRDATA+0(7),LCSCNTL     IS IT AN LCS RECORD?
         BNE   LCS0USR1            NO, BRANCH
         SPACE 1
         LA    R6,MSGLCS0U
         BAS   R4,LCS0HDR
         M$ERRST MSGBLANK
         OI    FLAGSCC,F1IDR
         LA    R6,MSGC1U
         BAS   R4,LCS0HDR
         OI    FLAGSCC,F1IDR
         LA    R6,MSGC2U
         BAS   R4,LCS0HDR
         MVC   MSGTEXT1(136),MSGBL132  CLEAR THE MESSAGE AREA
         MVC   MSGTEXT1+46(13),IDRDATA+27
         LA    R4,MSGTEXT1+46  ADDRESS OF LIBRARY
LCSSCAN  CLI   0(R4),X'40'    IS IT A SPACE
         BE    LCSSCANE       YES THEN END SCAN MOVE IN PANVALET
         LA    R4,1(R4)       INCREMENT
         B     LCSSCAN        CONTINUE SCAN
LCSSCANE MVC   0(9,R4),PANVALET
         MVC   MSGTEXT1+8(8),IDRDATA+18
         MVC   PACK+6(1),=X'0F'        MOVE IN SIGN BIT
         MVC   PACK+0(1),IDRDATA+26    MOVE IN VERSION
         UNPK  UNPACK,PACK             UNPACK VERSION
         TR    UNPACK,TRTABLE          TRANSLATE TO HEX
         MVC   MSGTEXT1+19(2),UNPACK   MOVE IN VERSION
         XC    PACK,PACK               CLEAR PACK AREA
         XC    UNPACK,UNPACK           CLEAR UNPACK AREA
         MVC   PACK+6(1),=X'0F'        MOVE IN SIGN BIT
         MVC   PACK+0(6),IDRDATA+12    MOVE IN DATE TIME
         UNPK  UNPACK,PACK             UNPACK DATE TIME
         TR    UNPACK,TRTABLE          TRANSLATE TO HEX
         MVC   MSGTEXT1+25(2),UNPACK+0 MOVE IN MONTH
         MVC   MSGTEXT1+27(1),=C'/'
         MVC   MSGTEXT1+28(2),UNPACK+2 MOVE IN DAY
         MVC   MSGTEXT1+30(1),=C'/'
         MVC   MSGTEXT1+31(2),UNPACK+4 MOVE IN YEAR
         MVC   MSGTEXT1+35(2),UNPACK+6 MOVE IN TIME
         MVC   MSGTEXT1+37(1),=C':'
         MVC   MSGTEXT1+38(2),UNPACK+8 MOVE IN TIME
         MVC   MSGTEXT1+40(1),=C':'
         MVC   MSGTEXT1+41(2),UNPACK+10 MOVE IN TIME
         LA    R1,80                    MAX LENGTH OF 80
         SLL   R1,16
         ST    R1,MSGTEXT1
         M$ERRST MSGTEXT1
         M$ERRST MSGBLANK
         B     NEWCMD
LCS0USR2 MVC   MSGTEXT1(136),MSGBL132  CLEAR THE MESSAGE AREA
         LA    R1,80                   MAX LENGTH OF 80
         SLL   R1,16
         ST    R1,MSGTEXT1
         MVC   MSGTEXT1+4(8),DIRNAME
         MVC   MSGTEXT1+13(50),MSGNLCS
         M$ERRST MSGTEXT1
         B     NEWCMD                  GO FOR NEW COMMAND
         DROP  R2,R3
         EJECT
         SPACE 2
         PRINT NOGEN
LCSCNTL  DC    CL7'LCSCNTL'
PANVALET DC    CL9'.PANVALET'
HIGDUMMY DC    CL8'HIGDUMMY'
         SPACE 2
SPFSTAT3 DC    CL72'MEMBER      VER.MOD  CREATED   LAST MODIFIED  SIZE X
                INIT   MOD   ID'
MSGNLCS  DC    CL50'HAS NO LIBRARY CONTROL INFORMATION'
MSGLCS0U M$TX  'LCS-DISPLAY -'
MSGC1U   M$TX '     MEMBER  VERSION   DATE      TIME        PANVALET LIX
               BRARY'
MSGC2U   M$TX '    --------   --    --------  --------   --------------X
               --------'
         PRINT GEN
         SPACE 2
