PROC 0 PARM() DEBUG PANEL(RCQDATE)
  IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST PROMPT MSG
  /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
  /*                     DATE (WAS CALENDAR REVISED DATE)            */
  /*                                                                 */
  /*  DO JULIAN <-> GREGORIAN DATE CONVERSION AND DISPLAY 3-MONTH    */
  /*  PERPETUAL CALENDAR.  SHOW SPECIFIED MONTH AND MONTHS BEFORE    */
  /*  AND AFTER.  HANDLES YEARS 1583 - 9999.                         */
  /*  NOTE: GEORGORIAN CALENDAR STARTED OCTOBER 15, 1582 IN EUROPE   */
  /*        BUT DIDN'T START IN AMERICA UNTIL SEPTEMBER 14, 1752.    */
  /*        BUT OLD HISTORY DATES CONVERTED TO NEW SYTLE FORMAT.     */
  /* WRITTEN BY: MICHAEL E. THEYS           ROCKWELL/SWCC 12/04/87   */
  /*                                                                 */
  /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
  IF &STR(&PARM) = &STR(?) THEN DO
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(RCQDATE$)
    EXIT
  END

  SET M$=&STR( JANUARY .FEBRUARY .  MARCH  .  APRIL  .   MAY   +
             .  JUNE   .  JULY   . AUGUST  .SEPTEMBER. OCTOBER +
             .NOVEMBER .DECEMBER )
  /*                  JAN.FEB.MAR.APR.MAY.JUN.JUL.AUG.SEP.OCT.NOV.DEC */
  SET MOND = &STR(???.031.028.031.030.031.030.031.031.030.031.030.031.)
  SET JULD = &STR(000.031.059.090.120.151.181.212.243.273.304.334.365.)

  SET TODAY = &STR(&SYSDATE)                  /* FORMAT: MM/DD/YY */
  ISPEXEC VGET (ZSTDYEAR)
  SET RIYYYY= &STR(&ZSTDYEAR)
/*SET RIYYYY= &STR(19&SUBSTR(7:8,&TODAY))*/
  SET RIMM  = &STR(&SUBSTR(1:2,&TODAY))
  SET RIDD  = &STR(&SUBSTR(4:5,&TODAY))

AGAIN:+
  SET MSG =
  SET CURSOR =

  SET LEAP = N
  IF  &EVAL( &RIYYYY // 4)   = 0 THEN SET LEAP = Y
  IF  &EVAL( &RIYYYY // 100) = 0 THEN SET LEAP = N
  IF  &EVAL( &RIYYYY // 400) = 0 THEN SET LEAP = Y

  IF &LEAP = N AND &RIMM = 2 AND &RIDD = 29 THEN SET MSG = RCQMO007
  IF &LEAP = N AND &RIJJJ > 365             THEN SET MSG = RCQMO008
  IF &MSG ¬= THEN DO
    SET CURSOR = RID
    GOTO SHOW
  END

  IF &STR(&RIJJJ) =    THEN DO /* MM/DD/YYYY -> JJJ.YYYY   */
    SET RIJLN = &EVAL(&SUBSTR(4*&RIMM-3:4*&RIMM-1,&STR(&JULD))) + &RIDD
    IF &LEAP = Y AND (&RIMM > 2) THEN SET RIJLN = &EVAL(&RIJLN + 1)
  END
  ELSE                      DO /* JJJ.YYYY   -> MM/DD/YYYY */
    SET RIMM = 12
    SET RIDD =
    SET RIJLN = &RIJJJ
    DO WHILE (&RIDD = )
      SET JLN = &SUBSTR(4*&RIMM-3:4*&RIMM-1,&STR(&JULD))
      IF &LEAP = Y AND &RIMM > 2 THEN SET JLN = &JLN + 1
      IF &RIJJJ >  &JLN THEN SET RIDD = &EVAL(&RIJJJ - &JLN)
      ELSE SET RIMM = &RIMM - 1
    END
  END
  IF      &RIJLN < 10  THEN SET RIJLN = &STR(00&RIJLN)
  ELSE IF &RIJLN < 100 THEN SET RIJLN = &STR(0&RIJLN)

  SET V = 1
  SET RIMM  = &EVAL(&RIMM - 1)  /* SET FOR PREVIOUS MONTH */
  IF &RIMM < 1 THEN DO
    SET RIMM = 12
    SET RIYYYY = &EVAL(&RIYYYY - 1)
  END

/* DETERMINE THE DAY OF THE WEEK FOR THE FIRST DAY OF THE MONTH   */
DO WHILE (&V <= 4)   /* DO FOR FOUR MONTH PERIOD. CURRENT MO IS #2 */

  SET YY = &RIYYYY
  SET DD = &RIDD
  SET MM = &RIMM

  SET M = &EVAL(&MM)
  SET D = 1
  SET Y = &EVAL(&YY)
  IF &M = 1 | &M = 2 THEN DO/* CONVERT JAN,FEB TO M=13,14 FOR FORMULA */
    SET M = &EVAL(&M + 12)
    SET Y = &EVAL(&Y - 1)
  END

  SET N  = &EVAL(&D+(2*&M)+((3*(&M+1))/5)+&Y+(&Y/4)-(&Y/100)+(&Y/400)+2)
  SET DAY# = &EVAL(&N // 7)           /* 0=SAT,1=SUN,...,5=THUR,6=FRI */
  IF &DAY# = 0 THEN SET DAY# = 7      /*  MAKE SATURDAY DAY 7 OF WEEK */

  /* DEFINE NUMBER OF DAYS IN THE MONTH */
  SET MON# = &EVAL( &SUBSTR(4*&MM+1:4*&MM+3,&MOND) )
  SET LEAP = N
  IF  &EVAL( &YY // 4)   = 0 THEN SET LEAP = Y
  IF  &EVAL( &YY // 100) = 0 THEN SET LEAP = N
  IF  &EVAL( &YY // 400) = 0 THEN SET LEAP = Y
  IF &MM = 2 AND &LEAP = Y   THEN SET MON# = 29

  /* DAY# IS POSITION IN CALENDAR OF DAY 1, MON# IS TOTAL DAYS */
  SET K = 1
  SET D = 1
  DO WHILE (&K <= 37)
    IF (&K >= &DAY# AND &D <= &MON#) THEN DO
      IF &D <= 9 AND &V ¬= 2 THEN SET MK = &STR( &D)
      ELSE                        SET MK = &STR(&D)
      SET D = &D + 1
    END
    ELSE              SET MK = &STR(  )
    SET RI&V.M&K = &STR(&MK)
    /* STRING WEEK OF MONTH ONLY USED FOR OTHER THAN CURRENT MONTH */
    SET L = &EVAL( 1 + ((&K - 1) / 7))   /* SET LINE# ON CALENDAR  */
    IF      &EVAL(&K // 7) = 1           THEN SET MLINE = &STR( &MK)
    ELSE IF &EVAL(&K // 7) = 0 | &K = 37 THEN SET RI&V.L&L = +
                                                  &STR(&MLINE &MK)
    ELSE                                      SET MLINE = +
                                                  &STR(&MLINE &MK)
    SET K = &K + 1
  END

  SET RI&V.Y    = &STR(&YY)
  SET RI&V.M#   = &STR(&MM)
  SET RI&V.MNTH = &SUBSTR(10*&MM-9:10*&MM-1,&STR(&M$))

  SET V = &V + 1   /* SET FOR THE NEXT MONTH TO DISPLAY */
  SET RIMM = &EVAL(&RIMM + 1)
  IF &RIMM > 12 THEN DO
    SET RIMM   = 1
    SET RIYYYY = &EVAL(&RIYYYY + 1)
  END

END

SHOW:+
ISPEXEC DISPLAY PANEL(&PANEL ) MSG(&MSG) CURSOR(&CURSOR)
IF &LASTCC >= 8 THEN EXIT
ISPEXEC VGET (ACTION) SHARED
SET RCQDATE =

IF &ACTION ¬= THEN DO
  SET YEAR  = &RI2Y
  SET MONTH = &RI2M#
  IF      (&ACTION = LEFT ) THEN DO
    SET YEAR = &YEAR - 1
  END
  ELSE IF (&ACTION = RIGHT) THEN DO
    SET YEAR = &YEAR + 1
  END
  ELSE IF (&ACTION = UP   ) THEN DO
    SET MONTH = &MONTH - 1
    IF &MONTH < 1  THEN DO
      SET MONTH = 12
      SET YEAR = &YEAR - 1
    END
  END
  ELSE IF (&ACTION = DOWN ) THEN DO
    SET MONTH = &MONTH + 1
    IF &MONTH > 12 THEN DO
      SET MONTH = 1
      SET YEAR = &YEAR + 1
    END
  END
  IF &YEAR > 9999 THEN DO
    SET YEAR = 9999
    SET MONTH = 12
  END
  IF &YEAR < 1583 THEN DO
    SET YEAR = 1583
    SET MONTH = 1
  END
  /* UPDATE THE PANEL INPUT DATE */
  SET RID    = 1
  SET RIM    = &MONTH
  SET RIYR   = &YEAR
  /* UPDATE THE WORK DATE */
  SET RIMM   = &MONTH
  SET RIDD   = 1
  SET RIJJJ  =
  SET RIYYYY = &YEAR
END

GOTO AGAIN
/* MICHAEL THEYS, MTHEYS2@CSC.COM */
/* CLIST TO DISPLAY PERPETUAL CALENDAR AND DATE CONVERSION */
