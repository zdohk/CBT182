*                                                             DRK JUN16
*        ISPF DSINFO CALL                                     DRK JUN16
*                                                             DRK JUN16
         TM    SPFLAG0,SPFDON         ISPMODE ACTIVE?
         BNO   DSI300C                NO, BRANCH
         LA    R14,DSINFO2            DSINFO
         ST    R14,PARMLIST           1ST PARM
         LA    R15,#USADSN
         MVI   0(R15),C''''           START QUOTE
         LA    R15,1(R15)
         MVC   0(44,R15),DSNAME       DSNAME
         AH    R15,DSNLEN
         MVI   0(R15),C''''           END QUOTE
         LA    R15,#USADSN            DSN
         ST    R15,PARMLIST+4         2ND PARM
         OI    PARMLIST+4,X'80'       MARK END OF PARM LIST
         LA    R1,PARMLIST            PT TO PARMLIST
         L     R15,ISPLINK
         BASR  R14,R15                CALL ISPF
         LTR   R15,R15                NORMAL COMPLETION?
         BNZ   DSI300C                NO
         SPACE 1
         LA    R14,VCOPY2             VCOPY
         ST    R14,PARMLIST           1ST PARM
         LA    R14,ZPDSE              GET PDSE VERSION, MAXGENS
         MVC   DOUBLE(8),VARLEN2      MOVE LENGTH ARRAY
         LA    R15,DOUBLE             LENGTH OF VARIABLES
         LA    R0,PDSEFORM            RETURN AREA
         LA    R1,ISPMOVE2            MOVE
         STM   R14,R1,PARMLIST+4      2ND-5TH PARMS
         OI    PARMLIST+16,X'80'      MARK END OF PARM LIST
         LA    R1,PARMLIST            PT TO PARMLIST
         L     R15,ISPLINK
         BASR  R14,R15                CALL ISPF
         LTR   R15,R15                NORMAL COMPLETION?
         BNZ   DSI300C                NO
         MVI   MTLEN,39               YES
         MVC   INSERT#1(39),MSG192H1
         M$MSG L192$1
         MVC   INSERT#1(39),BLANK128
         MVC   INSERT#1+24(1),PDSEFORM      ZDSDSNV
         MVC   INSERT#1+29(10),PDSEFORM+10  ZDSNGEN
         M$MSG L192$1
         M$ERRST MSGBLANK             ONE BLANK LINE
         CLI   PDSEFORM,C'1'          PDSE V1?                DRK APR18
         BE    DSI300C                YES, DONE               DRK APR18
         CLC   PDSEFORM+10(10),ZGENS  V2 AND ZERO GENS?       DRK APR18
         BE    DSI300C                YES, DONE               DRK APR18
         M$MSG L198                   NO, GIVE WARNING        DRK APR18
         M$ERRST MSGBLANK             ONE BLANK LINE          DRK APR18
         B     DSI300C                SKIP CONSTANTS
         SPACE 1
VARLEN2  DC    F'10',F'10'            LENGTH OF VARS
ISPMOVE2 DC    CL4'MOVE'              ACTION
VCOPY2   DC    CL8'VCOPY'             SERVICE
ZPDSE    DC    C'(ZDSDSNV ZDSNGEN)'   VARIABLE NAMES
DSINFO2  DC    CL8'DSINFO'            SERVICE
L192$1   DC    C'192'
L198     DC    C'198'                                         DRK APR18
ZGENS    DC    CL9' ',C'0'
MSG192H1 DC    CL39'PDSE FORMAT:      VERSION       MAXGENS'
*                                  1234567890    1234567890
DSI300C  DS    0H
