./ ADD NAME=QWIKDOC  LOAD these members for a quick install of PDS
./ ADD NAME=FIXPDSX  0102-17304-17304-0712-00024-00023-00000-T311LBD 08
/* --------------------  rexx procedure  -------------------- *
 | Name:      fixpdsx                                         |
 |                                                            |
 | Function:  ISPF Edit Macro to update the pds86             |
 |            elements                                        |
 |                                                            |
 | Usage Notes: called by the PDSQINST process out of the     |
 |              distribution pds                              |
 |                                                            |
 | Author:    Lionel B. Dyck                                  |
 |                                                            |
 | History:  (most recent on top)                             |
 |            10/31/17 - Creation                             |
 |                                                            |
 * ---------------------------------------------------------- */
 Address ISREdit
 'Macro (hlq)'
 '(member) = member'
  if wordpos(member,'PDS86X') > 0 then do
     "c 'hlq.' '"hlq".' all"
say 'Changing hlq to' hlq 'rc:' rc
     'save'
     'end'
     end
./ ADD NAME=PDSQINST 0112-17304-17304-0719-00148-00122-00000-T311LBD 28
/* --------------------  rexx procedure  -------------------- *
 | Name:      PDSQINST                                        |
 |                                                            |
 | Function:  PDS 8.6 Quick Install                           |
 |                                                            |
 | Syntax:    %PDSQINST                                       |
 |                                                            |
 | Dependencies: Must be run under ISPF from the PDS          |
 |               distribution library                         |
 |                                                            |
 | Author:    Lionel B. Dyck                                  |
 |                                                            |
 | History:  (most recent on top)                             |
 |            10/31/17 - Creation (LBD)                       |
 |                                                            |
 * ---------------------------------------------------------- */
 parse source x1 x2 x3 x4 pds86dsn .

/* --------------------------------- *
 | Validate that a dsname was passed |
 * --------------------------------- */
  if pds86dsn = '' then do
    say 'This exec requires the dataset name of the pds 8.6' ,
      'distribution dataset.'
    say 'Try again . . .'
    exit 8
  end
  else do
       fixexec = "'"pds86dsn"(pds86x)'"
       pds86dsn = "'"pds86dsn"'"
       end

/* ---------------------------------------- *
 | Now make sure the passed dsname is valid |
 * ---------------------------------------- */
  if sysdsn(pds86dsn) /= 'OK' then do
    say 'PDS 8.6 supplied distribution dataset name:' pds86dsn
    say 'Error:' sysdsn(pds86dsn)
    say 'Try again . . .'
    exit 12
  end

/* ----------------------------------------------- *
 | Now qualify the passed dsname and then validate |
 | that it is a PDS 8.6 distribution dataset.      |
 * ----------------------------------------------- */
  if left(pds86dsn,1) = "'"
  then p86dsn = substr(pds86dsn,2,length(pds86dsn)-2)
  else do
    if sysvar('syspref') = ''
    then p86dsn = pds86dsn
    else p86dsn = sysvar('syspref')'.'pds86dsn
  end
  if sysdsn("'"p86dsn"($$$INST)'") /= 'OK' then do
    say pds86dsn 'is not a valid PDS 8.6 distribution dataset.'
    say 'Try again. . . '
    exit 16
  end

/* ------------------------- *
 | Prompt for the target HLQ |
 * ------------------------- */
  x = PROMPT("ON") /* enable prompting */
  if sysvar('syspref') /= '' then dhlq = sysvar('syspref')
  else dhlq = sysvar('sysuid')
  say 'Enter the HLQ for the PDS 8.6 datasets to be created' ,
    'or blank for:' dhlq
  pull hlq
  if strip(hlq) = '' then hlq = dhlq
  say 'Target datasets will be created under HLQ:' hlq'.NEW.PDS86'

/* -------------------------------- *
 | Ask about Point and Shoot Panels |
 * -------------------------------- */
 say 'Enter any non-blank character to install the'
 say 'Lionel Dyck dynamic "Point and Shoot" ISPF Panels:'
 pull pnsp
 pnsp = strip(pnsp)

/* ---------------------------- *
 | Construct the target dsnames |
 * ---------------------------- */
  exec   = "'"hlq".NEW.PDS86.EXEC'"
  load   = "'"hlq".NEW.PDS86.LOAD'"
  msgs   = "'"hlq".NEW.PDS86.MSGS'"
  panels = "'"hlq".NEW.PDS86.PANELS'"

/* -------------------------- *
 | Test for existing datasets |
 * -------------------------- */
 if 'OK' = sysdsn(exec) then do
    say 'The target datasets already exist'
    say 'Delete them or select a new HLQ.'
    say 'Exiting . . .'
    exit 8
    end

/* ------------------------------- *
 | And Allocate the Target dsnames |
 * ------------------------------- */
  'Alloc ds('panels') new spa(120,90) tr dir(120)' ,
    'recfm(f b) lrecl(80) blksize(0)'
  'Alloc ds('exec') new spa(15,15) tr dir(56)' ,
    'recfm(f b) lrecl(80) blksize(0)'
  'Alloc ds('msgs') new spa(15,15) tr dir(12)' ,
    'recfm(f b) lrecl(80) blksize(0)'
  'Alloc ds('load') new spa(90,15) tr dir(52)' ,
    'recfm(u) blksize(32760)'
  'Free ds('panels msgs load')'

/* ------------------------ *
 | Receive the XMIT members |
 * ------------------------ */
  queue "dsn("load")"
  "Receive inds('"p86dsn"(UTILXMIT)')"
  queue "dsn("load")"
  "Receive inds('"p86dsn"(Z035XMIT)')"

/* ------------------ *
 | Get into ISPF Mode |
 * ------------------ */
  Address ISPExec
  'Control Errors Return'
  'libdef ispllib dataset id('load') stack'
  'Select cmd(pds86' pds86dsn 'copy (p86* pds@prim)' panels ' shr)'
  'Select cmd(pds86' pds86dsn 'copy pdsm86' msgs ' shr)'
  rexx = '(pds86x fixpdsx)'
  'Select cmd(pds86' pds86dsn 'copy' rexx exec ' shr)'
  if pnsp /= '' then do
     'Select cmd(pds86' pds86dsn 'load lbdpspan' panels 'shr)'
     'Select cmd(pds86' panels 'del lbd*)'
     end
  'libdef ispllib'

  Address TSO ,
      "altlib activate dataset("exec") application(exec)"
  "Edit Dataset("fixexec") macro(fixpdsx)" ,
        'parm(hlq)'
  Address TSO
  'altlib deactivate application(exec)'
  'Delete' fixpdsx
  Address ISPExec

 /* -------------------------------- *
  | Now display the target libraries |
  * -------------------------------- */
  "LMDINIT LISTID(LISTID) LEVEL("hlq".NEW.PDS86)"
  "LMDDISP LISTID("ListId") Confirm(Yes)",
    "View(Volume)"
  "LMDFREE LISTID("ListId")"
./ ADD NAME=PDS86X   0104-17304-17304-0723-00028-00028-00000-T311LBD 36
/* --------------------  rexx procedure  -------------------- *
 * Name:      pds86x                                          *
 *                                                            *
 * Function:  dynamically allocate the pds86 libraries        *
 *            for testing purposes                            *
 *                                                            *
 * Syntax:    %pds86x options                                 *
 *                                                            *
 * Author:    Lionel B. Dyck                                  *
 *                                                            *
 * History:                                                   *
 *            10/31/17 - Creation                             *
 *                                                            *
 * ---------------------------------------------------------- */
 arg options

 msgs   = "'hlq.new.pds86.msgs'"   /* changed during by pdsqinst use */
 load   = "'hlq.new.pds86.load'"   /* changed during by pdsqinst use */
 panels = "'hlq.new.pds86.panels'" /* changed during by pdsqinst use */

 address ispexec
 'libdef ispllib dataset id('load') stack'
 'libdef ispmlib dataset id('msgs') stack'
 'libdef ispplib dataset id('panels') stack'
 'Select cmd(pds86' options ') scrname(pds86)'
 'libdef ispllib'
 'libdef ispmlib'
 'libdef ispplib'
