         TITLE 'P D S  --  MATCH SUBLIST BUILD               11/29/15'
***********************************************************************
***      MATCH SUBLIST SUBROUTINE                                   ***
***********************************************************************
*
         SPACE 1
SAMLIST  DS    0H                                             DRK NOV15
         SR    R2,R2                      COUNTER
         MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)   CLOSE THE SYSPRINT FILE
         LA    R1,BAMDCB
         FREEPOOL (1)
         SPACE 1
         MVC   BAMDCB(MATDL2),MATDCB2     GET MODEL DCB FOR SAMLIST
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),=CL8'SYSPRINT'
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB),MF=(E,OPENLIST)     OPEN SYSPRINT
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10' SUCCESSFUL OPEN?
         BO    MAS010                       YES, BRANCH
         LA    R1,MAT865E                   NO STATUS AVAILABLE
         M$ERRST (R1)                       OUTPUT THE MESSAGE
         B     MAT8550                      EXIT
         SPACE 1
*  PROCESS SYSPRINT RECORDS FOR SAMLIST
MAS010   DS    0H
         GET   BAMDCB                     GET SYSPRINT
         CLC   1(11,R1),MASEXC1           HEADER?
         BE    MAS010                     YES, SKIP
         CLC   1(11,R1),MASEXC2           HEADER?
         BE    MAS010                     YES, SKIP
         CLC   1(11,R1),MASEXC3           HEADER?
         BE    MAS010                     YES, SKIP
         CLC   1(11,R1),MASEXC4           HEADER?
         BE    MAS010                     YES, SKIP
         CLC   1(11,R1),MASEXC5           HEADER?
         BE    MAS010                     YES, SKIP
         CLC   1(120,R1),BLANK128         BLANK LINE?
         BE    MAS010                     YES, SKIP
         SPACE 1
* TEST FOR UNEQUAL MESSAGES
         CLC   11(9,R1),MASUNEQ           <UNEQUAL>?
         BE    MAS015                     YES
         CLC   11(9,R1),MASUNDR           *********?
         BE    MAS015                     YES
         CLC   11(9,R1),MASNEW1           <-- NEWER?
         BE    MAS015                     YES
         CLC   11(9,R1),MASNEW2           NEWER -->?
         BE    MAS015                     YES
         CLC   39(16,R1),MASLIB1          IN LIBRARY1 ONLY?
         BE    MAS015                     YES
* TEST FOR EQUAL   MESSAGES
         CLC   1(8,R1),22(R1)             MEMBER NAMES EQUAL?
         BE    MAS015                     YES
         B     MAS010                     NO
MAS015   DS    0H
         LA    R2,1(R2)                   BUMP COUNTER
         MVC   DIRNAME(8),1(R1)           MEMBER NAME TO BLDLLIST
         SPACE 2
         SR    R15,R15
         BLDL  INDCB,BLDLLIST             LOCATE DIRECTORY ENTRY
         B     *+4(R15)                   PROCESS RETURN CODE
         B     MAS020                       00 - SUCCESSFUL
         B     MAS017                       04 - MEMBER NOT FOUND
         B     IOERROR                      08 - I/O ERROR
MAS017   DS    0H
         LA    R1,L853$1              MSG - (MEMBER) NOT FOUND
         MVC   INSERT#1(8),DIRNAME
         M$MSG (R1)                   OUTPUT MESSAGE
         B     MAS010
         SPACE 1
MAS020   DS    0H
         TM    FLAGSEE,FMEMGRP            MEMBER GROUP DEFINE START?
         BO    MAS100                     YES, BRANCH
         SPACE 1
         OI    FLAGSEE,FMEMGRP            FIRST SUBLIST ELEMENT
         MVC   MMEMMAX(4),MMEMMIN         POINT TO THE FIRST ELEMENT
         XC    MMEMBERD(1+2+8+8+2),MMEMBERD  CLEAR THE MEMBER NAME
         MVC   MMEMBERD+1+2(8),DIRNAME    MEMBER NAME
         MVC   MMEMBERD+1+2+8+8+2(3),DIRTTR TTR OF FIRST MEMBER
         LA    R1,DIRNAME+7               END OF MEMBER NAME
         LA    R0,7                       MACHINE LENGTH
MAS050   CLI   0(R1),X'40'                BLANK?
         BNE   MAS060                     NO, BRANCH
         BCTR  R1,0                       PREVIOUS CHARACTER
         BCT   R0,MAS050                  CHECK ALL BYTES
MAS060   STCM  R0,B'1100',MMEMBERD+1      SAVE THE MACHINE LENGTH
         MVI   MMEMBERD,FMEMBER1          A SINGLE MEMBER NAME
         NI    MMEMMIN,FF-X'80'           MEMBER LIST IS INACTIVE
         SPACE 1
         ICM   R1,B'0111',MMEMMIN+1       FIRST ELEMENT POINTER?
         BZ    MAS010                     NO, DONE
         MVI   0(R1),0                    YES, INACTIVE ELEMENT
         B     MAS010
         SPACE 1
MAS100   L     R1,MMEMMAX                 CURRENT END POINTER
         CLC   1(3,R1),ZERO               ANY SLOT ENTRIES LEFT?
         BNE   MAS180                     YES, BRANCH
         LA    R0,36*28                   36 MEMBERS, 28 WIDE EACH
         ICM   R0,B'1000',=AL1(24)        ASSUME SPF IS INITIALIZED
         STCM  R0,B'1000',SUBLIST2        MARK FOR LATER RELEASE
         TM    FLAGSFF,FSPFCALL+FSPFDIAL  SPF INITIALIZED?
         BM    MAS110                     YES, BRANCH
         AIF   ('&CISP' NE 'ISPFV2').NSPFME3
         TM    FLAGSFF,FSPFOPT6           CALLED FROM OPTION 6?
         BO    MAS110                     YES, BRANCH
.NSPFME3 ANOP
         ICM   R0,B'1000',=AL1(23)        SPF IS NOT INITIALIZED
         STCM  R0,B'1000',SUBLIST1        MARK FOR LATER RELEASE
         MVI   SUBLIST2,0                 CLEAR THE EARLIER MARK
MAS110   GETMAIN R,LV=(0)                 GET MORE ELEMENT STORAGE
         LR    R15,R1                     START OF TABLE
         L     R1,MMEMMAX                 CURRENT ELEMENT
         LA    R0,36-1                    NUMBER OF ELEMENTS -1
MAS136   LA    R15,28(,R15)               NEXT ELEMENT POINTER
         ST    R15,0(,R1)                 POINT TO THE NEXT ELEMENT
         LR    R1,R15                     NEXT ELEMENT
         BCT   R0,MAS136                  DO 36 ELEMENTS
         XC    0(4,R1),0(R1)              CLEAR THE LAST POINTER
         SPACE 2
MAS180   LA    R1,MMEMMIN                 ANCHOR FOR MEMBER LIST
         CLC   DIRNAME(8),MMEMBERD+1+2    ALREADY IN LIST?
         BE    MAS010                     YES, DO NOT ADD AGAIN
         SPACE 2
MAS190   L     R1,0(,R1)                  NEXT ELEMENT
         TM    0(R1),X'80'                ACTIVE ELEMENT?
         BNO   MAS200                     NO, NOT FOUND
         CLC   DIRNAME(8),4+2(R1)         ALREADY IN LIST?
         BE    MAS010                     YES, DO NOT ADD AGAIN
         B     MAS190                     SEARCH THE ENTIRE LIST
         SPACE 2
MAS200   L     R1,MMEMMAX                 CURRENT ADDED ELEMENT
         L     R15,0(,R1)                 NEXT POINTER
         MVI   0(R15),0                   INACTIVE THE NEXT ELEMENT
         ST    R15,MMEMMAX                NEXT ELEMENT TO ADD
         OI    0(R1),X'80'                ACTIVE ELEMENT
         OI    MMEMMIN,X'80'              MEMBER LIST IS ACTIVE
         XC    4(2+8+8+2,R1),4(R1)        CLEAR THE MEMBER NAME
         MVC   4+2(8,R1),DIRNAME          MEMBER NAME
         MVC   4+2+8+8+2(3,R1),DIRTTR     ADD THIS TTR
         LA    R15,DIRNAME+7              END OF MEMBER NAME
         LA    R0,7                       MACHINE LENGTH
MAS250   CLI   0(R15),X'40'               BLANK?
         BNE   MAS260                     NO, BRANCH
         BCTR  R15,0                      PREVIOUS CHARACTER
         BCT   R0,MAS250                  CHECK ALL BYTES
MAS260   STC   R0,5(R1)                   SAVE THE MACHINE LENGTH
         MVI   4(R1),FMEMBER1             A SINGLE MEMBER NAME
         B     MAS010
MAS900   DS    0H
         LTR   R2,R2                   COUNTER ZERO?
         BZ    MAS910                  YES
         MVC   PMEMCURR(4),PMEMMIN     RESET THE CURRENT POINTER
         MVC   LMEMBER1(2+8+8+2),MMEMBERD+1  RESET THE MEMBER NAME
         MVC   FLAGSAA(1),MMEMBERD     RESET THE MEMBER FLAG
         XC    MMEMMIN(12),PMEMMIN     INTERCHANGE
         XC    PMEMMIN(12),MMEMMIN                THE
         XC    MMEMMIN(12),PMEMMIN                   MEMBER LISTS
         MVC   MEMBERD(1+2+8+8+2),MMEMBERD    RESET THE DEFAULT NAME
         BAS   R14,DEFGROUP            ADD DEFAULT GROUP
         XI    FLAGSEE,FMEMGRP         MEMGRP USED NOW
         MVI   FLAGSAA,00              RESET THE MEMBER FLAG
         B     MAS920
MAS910   DS    0H
         MVI   FLAGSAA,0                  NULL MEMBER GROUP
         MVC   PMEMCURR(4),PMEMMAX        NULL MEMBER LIST
         NI    PMEMMIN,255-X'80'          NULL MEMBER LIST
         XC    MEMBERD(1+2+8+8+2),MEMBERD NO MEMBER LIST
         L     R1,MSGENTPT                START OF MEMBER PART
         MVC   0(17,R1),BLANK128          CLEAR OUT THE MEMBER NAME
MAS920   DS    0H
         B     MAT8510                 DONE ...
         SPACE 1
MASUNEQ  DC    CL9'<UNEQUAL>'          NEONLY/DATA
MASUNDR  DC    CL9'*********'          NEONLY/USER
MASNEW1  DC    CL9'<-- NEWER'          NEONLY/USER
MASNEW2  DC    CL9'NEWER -->'          NEONLY/USER
MASLIB1  DC    CL16'IN LIBRARY1 ONLY'  NEONLY/DATA | USER
*
MASEXC1  DC    CL11'--- PDSMATC'       SKIP HEADER LINES
MASEXC2  DC    CL11'CRITERIA FO'       "    "      "
MASEXC3  DC    CL11'(SPF STATIS'       "    "      "
MASEXC4  DC    CL11'LIBRARY1 - '       "    "      "
MASEXC5  DC    CL11'LIBRARY2 - '       "    "      "
         SPACE 1
         CNOP  0,8
         SPACE 3
