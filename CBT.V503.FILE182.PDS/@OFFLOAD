         TITLE 'P D S  --  OFFLOAD INTERFACE                  5/22/06'
***********************************************************************
***      OFFLOAD SUBCOMMAND   ADDED BY JOHN KALINICH -- MAY., 2006  ***
***********************************************************************
*
OFFLOAD  CSECT
         USING *,R8
         SPACE 1
*  FREE SYSIN, SYSUT1, SYSUT2, AND SYSPRINT DATASETS
         LA    R1,=CL8'SYSIN'           SYSIN DDNAME
         BAS   R2,OFL8580               FREE THIS DATA SET
         LA    R1,SYSUT1                SYSUT1 DDNAME
         BAS   R2,OFL8580               FREE THIS DATA SET
         LA    R1,SYSUT2                SYSUT2 DDNAME
         BAS   R2,OFL8580               FREE THIS DATA SET
         LA    R1,=CL8'SYSPRINT'        SYSPRINT DDNAME
         BAS   R2,OFL8580               FREE THIS DATA SET
*  ALLOCATE OFFLOAD //SYSUT1 DATA SET   INPUT PDS
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'OFLDDNU1+6,R3),OFLDDNU1       SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLDDNU1+6(,R3)              START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLDSNAM,R3),OFLDSNAM         SPECIFY DSNAME TEXT
         MVC   6(44,R3),DSNAME                   MOVE IN DSNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLALLOC,R3),OFLALLOC         SPECIFY DISP TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLNORM,R3),OFLNORM           SPECIFY NORMAL TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLANORM,R3),OFLANORM         SPECIFY ABNORMAL TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         SPACE 1
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   OFLERROR                      NO, BRANCH
         SPACE 2
*  ALLOCATE OFFLOAD //SYSUT2 DATA SET   OUTPUT SEQ DSN
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'OFLDDNU2+6,R3),OFLDDNU2       SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLDDNU2+6(,R3)              START OF NEXT TEXT
         SPACE 1
OFL0000  DS    0H
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLDSNAM,R3),OFLDSNAM         SPECIFY DSNAME TEXT
*        MVC   6(44,R3),MSGDS2                   MOVE IN DSNAME TEXT
         SPACE 1
         LA    R2,5(,R3)                         WHERE TO STORE OUTPUT
         MVC   6(44,R3),BLANK128                 CLEAR DSNAME AREA
         MVC   DOUBLE(8),BLANK128                CLEAR MEMBER AREA
         LA    R1,MSGDS2                         DSN OR DSN(MBR)
         LA    R0,44
         CLI   0(R1),C''''                       QUOTED?
         BE    OFL0010                           YES, BRANCH
         SR    R14,R14                           NO
         L     R15,ADDRUPT                       START OF THE UPT
* 8-CHAR PREFIX                                               DRK JUL17
         TM    FLAGSZZ,TSO#8                     8-CHAR ID/PREFIX?    "
         BNO   OFL7PRE                           NO                   "
OFL8PRE  DS    0H                                YES                  "
         ICM   R14,B'0001',UPTPRF8L-UPT(R15)     PREFIX LENGTH ZERO?  "
         BZ    OFL0010+4                         YES, BRANCH          "
         MVC   6(8,R3),UPTPREF8-UPT(R15)         DSNAME PREFIX        "
         B     OFLCONT                                                "
OFL7PRE  DS    0H                                                     "
         ICM   R14,B'0001',UPTPREFL-UPT(R15)     PREFIX LENGTH ZERO?  "
         BZ    OFL0010+4                         YES, BRANCH          "
         MVC   6(7,R3),UPTPREFX-UPT(R15)         DSNAME PREFIX        "
OFLCONT  DS    0H                                             DRK JUL17
         AR    R2,R14                            POSITION FOR PERIOD
         SR    R0,R14
         MVI   1(R2),C'.'
         BCTR  R0,0
         BCTR  R1,0
         LA    R2,1(,R2)
         SPACE 1
OFL0010  LA    R1,1(,R1)                         NEXT INPUT BYTE
         LA    R2,1(,R2)                         NEXT OUTPUT BYTE
         CLI   0(R1),C''''                       END OF DSNAME?
         BE    OFL0050                           YES, BRANCH
         CLI   0(R1),C'('                        PARENTHESIS?
         BE    OFL0020                           YES, BRANCH
         CLI   0(R1),X'40'                       BLANK?
         BE    OFL0050                           YES, BRANCH
         MVC   0(1,R2),0(R1)                     MOVE IN ONE MORE BYTE
         BCT   R0,OFL0010
         B     OFL0050
         SPACE 1
OFL0020  MVC   DOUBLE(8),BLANK128                MEMBER NAME
         LA    R0,8
         LA    R2,DOUBLE-1
         SPACE 1
OFL0030  LA    R1,1(,R1)                         NEXT INPUT BYTE
         LA    R2,1(,R2)                         NEXT OUTPUT BYTE
         CLI   0(R1),C''''                       END OF DSNAME?
         BE    OFL0050                           YES, BRANCH
         CLI   0(R1),C')'                        PARENTHESIS?
         BE    OFL0050                           YES, BRANCH
         CLI   0(R1),X'40'                       BLANK?
         BE    OFL0050                           YES, BRANCH
         MVC   0(1,R2),0(R1)                     MOVE IN ONE MORE BYTE
         BCT   R0,OFL0030
OFL0050  DS    0H
         LA    R1,M99RBPTR                       RESET R1
         MVC   MSGDS2(44),6(R3)                  RESET THE DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLDSNAM+44(,R3)             START OF NEXT TEXT
OFL0060  DS    0H
         SPACE 1
* LOCATE OUTPUT DSNAME
         ST    R1,#SAVEREG              SAVE R1
         MVC   WORKTMP(44),MSGDS2       COPY DSNAME
         L     R14,LOCATE2
         LA    R15,WORKTMP              ACTUAL DSNAME
         SR    R0,R0                    NO CVOL VALUE
         LA    R1,MSGTEXT1              LOCATE WORK AREA
         STM   R14,R1,PARMLIST          SAVE CAMLIST PARAMETERS
         LOCATE PARMLIST
         CLI   #OFLDISP,4               NEW?
         BNE   OFLNOTNW                 NO
         LTR   R15,R15                  SUCCESSFUL LOCATE?
         BZ    OFLDSN                   YES, ERROR
         L     R1,#SAVEREG              RESTORE R1
         B     OFL0075                  NO, OK
OFLNOTNW DS    0H
         LTR   R15,R15                  SUCCESSFUL LOCATE?
         BNZ   OFLNODSN                 NO, ERROR
         SPACE 1
* OBTAIN DSCB
         L     R14,OBTAIN2              OBTAIN FLAGS
         LA    R15,WORKTMP              ACTUAL DSNAME
         LA    R0,MSGTEXT1+6            VOLUME SERIAL (FROM LOCATE)
         LA    R1,MSGTEXT2              OUTPUT START ADDRESS
         STM   R14,R1,PARMLIST          SAVE CAMLIST PARAMETERS
         OBTAIN PARMLIST                GET THE FORMAT 1 DSCB
         LTR   R15,R15                  SUCCESSFUL?
         BZ    OFLDSCB                  YES, BRANCH
         M$MSG L830                     NO DSCB FOUND MESSAGE
         MVC   PMEMCURR,=XL4'00'        CURRENT MEMBER POINTER
         NI    FLAGSAA,FF-FMEM#MEM      TURN OFF MEMBER GROUP
         NI    FLAGSII,FF-FDIRGRP       NORMAL INPUT NOW
         B     NEWCMD
OFLDSN   MVI   MTLEN,44                 LENGTH OF INSERT
         MVC   INSERT#1(44),MSGDS2      DSNAME
         M$MSG L852$1                   DSNAME DOES NOT EXIST
         MVC   PMEMCURR,=XL4'00'        CURRENT MEMBER POINTER
         NI    FLAGSAA,FF-FMEM#MEM      TURN OFF MEMBER GROUP
         NI    FLAGSII,FF-FDIRGRP       NORMAL INPUT NOW
         B     NEWCMD
OFLNODSN MVI   MTLEN,44                 LENGTH OF INSERT
         MVC   INSERT#1(44),MSGDS2      DSNAME
         M$MSG L844$1
         MVC   PMEMCURR,=XL4'00'        CURRENT MEMBER POINTER
         NI    FLAGSAA,FF-FMEM#MEM      TURN OFF MEMBER GROUP
         NI    FLAGSII,FF-FDIRGRP       NORMAL INPUT NOW
         B     NEWCMD
OFLDSCB  DS    0H
         CLI   MSGTEXT2+X'26',X'02'     DSORG PARTITIONED?
         BNE   OFL0065                  NO
         CLI   DOUBLE,C' '              YES, MEMBER SPECIFIED?
         BNE   OFL0065                       YES
         MVC   DOUBLE(8),$OFFLOAD            NO, GENERATE NAME
OFL0065  DS    0H
         SPACE 1
         L     R1,#SAVEREG                       RESTORE R1
         CLI   DOUBLE,C' '                       MEMBER NAME?
         BE    OFL0075                           NO
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLDSMBR,R3),OFLDSMBR         SPECIFY MEMBER TEXT
         MVC   6(8,R3),DOUBLE                    MOVE IN MEMBER TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLDSMBR+8(,R3)              START OF NEXT TEXT
OFL0075  DS    0H
* DISP                                                        DRK MAR18
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLALLOC,R3),OFLALLOC         SPECIFY DISP TEXT
         CLI   #OFLDISP,0                        ANY OLD/SHR/MOD/NEW?
         BNE   *+8                               YES, BRANCH
         MVI   #OFLDISP,2                        NO, USE DEFAULT
         SPACE 1
         CLI   #OFLDISP,1                        OLD?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),OFLOOLD                     SPECIFY DISPOSITION
         SPACE 1
         CLI   #OFLDISP,2                        SHR?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),OFLOSHR                     SPECIFY DISPOSITION
         SPACE 1
         CLI   #OFLDISP,3                        MOD?
         BNE   *+8+8                             NO, BRANCH
         CLI   MSGTEXT2+X'26',X'40'              DSORG SEQ?
         BNE   *+8                               NO
         MVI   6(R3),OFLOMOD                     SPECIFY DISPOSITION
         SPACE 1
         CLI   #OFLDISP,4                        NEW?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),OFLONEW                     SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
* DISP=NEW                                                    DRK MAR18
         CLI   #OFLDISP,4                        NEW?
         BL    OFLNW30                           NO, BRANCH
         SPACE 1
         CLI   #OFLVOL,0                         ANY VOLUME SERIAL?
         BE    OFLNW10                           NO, BRANCH
* VOLSER, UNIT                                                DRK MAR18
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLVOLSR,R3),OFLVOLSR         SPECIFY VOLUME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVC   6(6,R3),#OFLVOL                   MOVE VOLSER
         LA    R3,L'OFLVOLSR(,R3)                START OF NEXT TEXT
         SPACE 1
OFLNW10  DS    0H
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLUNITP+8,R3),OFLUNITP       SPECIFY UNIT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLUNITP+8(,R3)              START OF NEXT TEXT
* DSORG, RECFM, LRECL, BLKSIZE                                DRK MAR18
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLSDS+2,R3),OFLSDS           SPECIFY DSORG
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLSDS+2(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLSRF+1,R3),OFLSRF           SPECIFY RECFM
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'OFLSRF+1(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLSLR,R3),OFLSLR             SPECIFY LRECL
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVC   6(2,R3),LRECL                     MOVE LRECL
         LA    R3,L'OFLSLR+2(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLSBL,R3),OFLSBL             SPECIFY BLKSIZE
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVC   6(2,R3),BLKSI                     MOVE BLKSIZE
         LA    R3,L'OFLSBL+2(,R3)                START OF NEXT TEXT
         SPACE 1
* TRK/CYL ALLOCATION                                          DRK MAR18
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLTRK,R3),OFLTRK             SPECIFY TRACK ALLOC
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         CLI   #OFLTRCY,0                        TRK/CYL REQUESTED?
         BH    OFLNW15                           YES, BRANCH
         MVI   #OFLTRCY,1                        NO, DEFAULT TO TRK
OFLNW15  CLI   #OFLTRCY,1                        TRACK?
         BNE   *+8                               NO, BRANCH
         MVI   1(R3),X'07'                       YES, CONVERT TO TRK
         CLI   #OFLTRCY,2                        CYLINDER?
         BNE   *+8                               NO, BRANCH
         MVI   1(R3),X'08'                       YES, CONVERT TO CYL
         LA    R3,L'OFLTRK(,R3)                  START OF NEXT TEXT
         SPACE 1
* SPACE (P,S)                                                 DRK MAR18
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLFIRST+3,R3),OFLFIRST       SPECIFY PRIMARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SR    R14,R14
         ICM   R14,B'0011',#OFLSPAC              ANY PRIMARY SPACE?
         BNZ   OFLNW20                           YES, BRANCH
         B     OFLNW25                           NO, USE DEFAULT
OFLNW20  STCM  R14,B'0111',6(R3)                 UPDATE FIRST QUANTITY
OFLNW25  DS    0H
         LA    R3,L'OFLFIRST+3(,R3)              START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLSECON+3,R3),OFLSECON       SPECIFY SECONDARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SR    R14,R14
         ICM   R14,B'0011',#OFLSPAC+2            SECONDARY SPACE?
         BZ    OFLNW30                           NO
         SPACE 1
         STCM  R14,B'0111',6(R3)                 UPDATE 2ND QUANTITY
OFLNW30  DS    0H
         LA    R3,L'OFLSECON+3(,R3)              START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLCAT,R3),OFLCAT             DISP=(,CATLG)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         CLI   #OFLDISP,4                        NEW?
         BE    *+8                               YES, BRANCH
         MVI   6(R3),X'08'                       NO, USE DISP=(,KEEP)
         LA    R3,L'OFLCAT(,R3)                  START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'OFLCDEL,R3),OFLCDEL           DISP=(,,DELETE)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         CLI   #OFLDISP,4                        NEW?
         BE    *+8                               YES, BRANCH
         MVI   6(R3),X'08'                       NO, USE DISP=(,,KEEP)
         SPACE 1
OFLNW99  DS    0H
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         SPACE 1
         DYNALLOC
         DROP  R1,R4,R5
OFLDB01  DS    0H
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   OFLERROR                      NO, BRANCH
         CLI   #OFLDISP,4                    NEW?
         BNE   OFLDB02                       NO, BRANCH
         MVI   MTLEN,44                 LENGTH OF INSERT
         MVC   INSERT#1(44),MSGDS2      DSNAME
         M$MSG L273$1                   DISPLAY OUTPUT DSN *DEBUG*
OFLDB02  DS    0H
         SPACE 2
*  ALLOCATE OFFLOAD //SYSIN FILE
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(11),OFLDDNIN      SYSIN DDNAME
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(40),OFLSYSIN    ADD OTHER LIST ELEMENTS
         SPACE 1
         DYNALLOC
         SPACE 2
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   OFLERROR                   NO, BRANCH
         SPACE 3
*  ALLOCATE OFFLOAD //SYSPRINT DATA SET
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),OFLDDNPR      SYSPRINT DDNAME
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(40),OFLSYSPR    ADD OTHER LIST ELEMENTS
         SPACE 1
         DYNALLOC
         SPACE 2
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   OFLERROR                   NO, BRANCH
         B     OFL2100
         SPACE 2
OFLERROR ST    R15,DAIRRC                 SAVE RETURN CODE
         LA    R14,M99RB                  POINTER TO SVC 99 BLOCK
         LA    R15,DAIRRC                 POINTER TO RETURN CODE
         LA    R0,ADDRFF02                POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)           POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL                POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP             INITIALIZE DFDAPLP, DFDRCP,
*                                           DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                    PROBLEM WITH IKJEFF18?
         BZ    OFL8550                    NO, BRANCH          DRK MAR18
         SPACE 1
OFLDAIR  DS    0H
         CVD   R15,DOUBLE
         MVI   MTLEN,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         M$MSG L835$1
         B     OFL8550                    TERMINATE           DRK MAR18
         SPACE 3
OFL2100  MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    OFL2200               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    OFL2110               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    OFL2200               YES, BRANCH
OFL2110  TM    #OFLALI,#OFLASS2      ASSOCIATES TOO?
         BO    OFL2200               YES, BRANCH
         SPACE 1
OFL2120  DS    0H
         TM    #OFLALI,#OFLASS2+2    ALIAS/NOALIAS SPECIFIED?
         BM    OFL2130               YES
*        OI    #OFLALI,&OFLALI       USE THE DEFAULT
         OI    #OFLALI,2             USE THE DEFAULT          DRK MAY06
         TM    #OFLALI,#OFLASS2      ASSOCIATES TOO?
         BO    OFL2200               YES
         SPACE 3
OFL2130  DS    0H
         OI    #OFLALI,#OFLONE       ONLY A SINGLE MEMBER/DATA SET
         LA    R4,DDNAMEH            HOLD FOR THIS SUBCOMMAND
         MVC   0(8,R4),DIRNAME
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         B     OFL2900
OFL2200  OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         TM    #OFLALI,#OFLASS2+2    ALIAS/NOALIAS SPECIFIED?
         BM    *+8                   YES
*        OI    #OFLALI,&OFLALI       NO, USE THE DEFAULT
         OI    #OFLALI,2             NO, USE THE DEFAULT      DRK MAY06
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         SPACE 1
OFL2210  SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   OFL2220               YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     OFL2500
         SPACE 1
OFL2220  SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
OFL2300  TM    #OFLALI,#OFLASSP      ASSOCIATES PHASE?
         BO    OFL2320               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    OFL2320               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   OFL2500               NO, BRANCH
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    OFL2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BNO   OFL2605               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   OFL2605               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    OFL2319               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1       MOVE TO BLDLLIST
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     OFL2315                   00 - SUCCESSFUL
         B     OFL2310                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
OFL2310  MVC   INSERT#1(8),DIRNAME
         M$MSG L853$1                MSG - (MEMBER) NOT FOUND
         B     OFL2300
         SPACE 1
OFL2315  MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     OFL2500
         SPACE 1
OFL2319  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
OFL2320  BAS   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     OFL2600               END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #OFLALI,#OFLASSP      ASSOCIATES PHASE?
         BO    OFL2400               YES, BRANCH
         SPACE 1
         BAS   R2,MFILTER            CHECK MEMBER NAMES
         B     OFL2600                 00 - SIMULATE DIRECTORY END
         B     OFL2300                 04 - MEMBER FAILED FILTERING
         B     OFL2500                 08 - MEMBER PASSED FILTERING
         SPACE 3
OFL2400  L     R1,#MEMPTR            START OF MEMBER LIST
         B     OFL2410+4             SKIP FIRST ADD
OFL2410  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   OFL2300               YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   OFL2410               NO, BRANCH
         SPACE 3
OFL2500  L     R1,#MEMPTR            START OF MEMBER LIST
         B     OFL2510+4             SKIP FIRST ADD
OFL2510  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   OFL2580               YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    OFL2300               YES, IGNORE THIS ENTRY
         B     OFL2510               NO, CONTINUE SEARCHING
         SPACE 2
OFL2580  MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   OFL2210               NO, BRANCH
         B     OFL2300               YES, GET NEXT MEMBER
         SPACE 3
OFL2600  TM    #OFLALI,#OFLASSP      ASSOCIATED MEMBERS?
         BO    OFL2605               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    OFL2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    OFL2300               YES, BRANCH
         SPACE 1
OFL2605  TM    #OFLALI,#OFLASSP      ASSOCIATES PHASE STARTED?
         BO    OFL2700               YES, BRANCH
         TM    #OFLALI,#OFLASS2      ASSOCIATE MEMBERS TOO?
         BNO   OFL2700               NO, BRANCH
         OI    #OFLALI,#OFLASSP      YES, ASSOCIATES PHASE STARTED
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     OFL2300               DO ANY ASSOCIATED MEMBERS
         SPACE 3
OFL2700  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         SPACE 3
OFL2900  DS    0H
         ST    R4,#MEMLAST           SAVE LAST MEMBER POINTER
*  OPEN SYSIN DATA SET
         MVC   BAMDCB(OFLDL),OFLDCB           GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),=CL8'SYSIN'
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB,OUTPUT),MF=(E,OPENLIST) OPEN SYSIN
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'    SUCCESSFUL OPEN?
         BO    OFL2910                         YES, BRANCH
         LA    R1,OFL865E                      NO STATUS AVAILABLE
         M$ERRST (R1)                          OUTPUT THE MESSAGE
         B     OFL8550                         EXIT
         SPACE 3
OFL2910  DS    0H
         MVC   MSGTEXT1(136),MSGBL132   CLEAR THE MESSAGE AREA
         LA    R5,MSGTEXT1+4
         CLI   #OFLSEP,2                NOSEP?                DRK DEC15
         BNE   *+4+6+4                  NO                    DRK DEC15
         MVC   0(20,R5),OFLHDR          O I=SYSUT1,O=SYSUT2
         B     OFL2920
         MVC   0(31,R5),OFLHDR          O I=SYSUT1,O=SYSUT2,T=IEBUPDTE
OFL2920  DS    0H
         PUT   BAMDCB,MSGTEXT1+4        OUTPUT OFFLOAD STATEMENT
         SPACE 1
OFL3070  DS    0H
         TM    FLAGSHH,FMEMALLM         ALL MEMBERS?
         BNO   OFL4000                  NO
         B     OFL4540                  YES
OFL4000  DS    0H
         L     R3,#MEMPTR                 FIRST MEMBER POINTER
         L     R4,#MEMLAST                LAST MEMBER POINTER
         MVC   MSGTEXT1(136),MSGBL132     CLEAR THE MESSAGE AREA
         SPACE 1
OFL4530  CR    R3,R4                      END OF MEMBER TABLE?
         BNL   OFL4540                    YES
         LA    R5,MSGTEXT1+4
         MVC   0(5,R5),SELECT             SELECT STATEMENT
         LA    R5,5(R5)
         MVC   0(8,R5),0(R3)              MEMBER NAME
         PUT   BAMDCB,MSGTEXT1+4          OUTPUT SELECT STATEMENT
         LA    R3,16(,R3)                 NEXT MEMBER POSITION
         B     OFL4530
         SPACE 1
OFL4540  DS    0H
         MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)   CLOSE SYSIN
         LA    R1,BAMDCB
         FREEPOOL (1)
         SPACE 2
         OI    ##ADRPA#,$A                DEFER ALL ATTENTIONS
         STAX  OFFLNULL,USADDR=(R7),REPLACE=NO,IBUF=0,                 X
               OBUF=(OFL484W,OFL484WL),MF=(E,STAXPARM)
         LA    R1,OFLPARM                 PARM=
         ST    R1,#CMXLIST
         OI    #CMXLIST,X'80'             MARK END OF LIST
         XC    #SUBHOLD(28+20),#SUBHOLD
         LA    R14,#SUBHOLD+28          1 POINT TO FLAGS
         LA    R15,OFLPGM               2 POINT TO CALLED PROGRAM
         LA    R0,#SUBHOLD+28+4         3 POINT TO PROGRAM NAME LENGTH
         LA    R1,#SUBHOLD+28+8         4 POINT TO RETURN CODE
         LA    R2,#SUBHOLD+28+12        5 POINT TO REASON CODE
         LA    R3,#SUBHOLD+28+16        6 POINT TO ABEND CODE
         LA    R4,#CMXLIST              7 POINT TO PARAMETER LIST
         STM   R14,R4,#SUBHOLD            UPDATE IKJEFTSR PARM LIST
         OI    #SUBHOLD+24,X'80'          INDICATE THE END OF THE LIST
         MVI   #SUBHOLD+28+3,X'02'        A PROGRAM IS BEING INVOKED
         MVI   #SUBHOLD+28+4+3,8          PROGRAM NAME LENGTH IS 8
         SPACE 2
         L     R2,RECOVER
         LA    R1,OFL7080                 RECOVERY ADDRESS
         ST    R1,RECOVER
         STM   R2,R8,#SUBHOLD+88          SAVE REGISTERS
         SPACE 1
         LA    R3,##SUBCAL                WHO TO ATTACH
         LA    R1,#CMXLIST                PARAMETERS TO PASS
         CLC   ##SUBCAL(8),OFLEFTSR       INVOKE IKJEFTSR?
         BNE   OFLNLNK                    NO, BRANCH
         LA    R1,#SUBHOLD                POINT TO THE PARAMETER LIST
         SPACE 2
OFLNLNK  BAS   R2,ATTACH                  OFFLOAD
         SPACE 3
OFL7080  LM    R2,R8,#SUBHOLD+88
         ST    R2,RECOVER                 RESET RECOVERY ADDRESS
         STAX  ,
         NI    ##ADRPA#,FF-$A             ALLOW ATTENTIONS AGAIN
         MVI   ATTNECB,0                  CLEAR THE ATTENTION ECB
         MVC   SUBSECB+3(1),#SUBHOLD+28+11  COPY FINAL RETURN CODE
         SPACE 1
         L     R2,#SUBHOLD+28+12          LOAD REASON CODE    DRK JAN07
         C     R2,=F'40'                  PROGRAM NOT FOUND?  DRK JAN07
         BNE   OFL8000                    NO                  DRK JAN07
         MVC   INSERT#1(8),OFLPGM         YES, ERROR          DRK JAN07
         M$MSG L859$1                     NOT FOUND MESSAGE   DRK JAN07
         B     OFL8550                                        DRK JAN07
         SPACE 1
OFL8000  MVC   BAMDCB(OFLDL),OFLDCB         GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),=CL8'SYSPRINT'
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB),MF=(E,OPENLIST)     OPEN SYSPRINT
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10' SUCCESSFUL OPEN?
         BO    OFL8010                      YES, BRANCH
         LA    R1,OFL865E                   NO STATUS AVAILABLE
         M$ERRST (R1)                       OUTPUT THE MESSAGE
         B     OFL8550                      EXIT
         SPACE 3
*  PROCESS SYSPRINT RECORDS
OFL8010  GET   BAMDCB                     GET LIST RECORD
         MVC   MSGTEXT1(136),MSGBL132     BLANK THE MESSAGE LINE
         SR    R15,R15
         ICM   R15,B'0011',DCBLRECL-IHADCB+BAMDCB  RECFM=UA
         SH    R15,=H'2'                  -2 (MACHINE LENGTH AND ASA)
         MVC   MSGTEXT1+4(*-*),1(R1)      << EXECUTED >>
         EX    R15,*-6                    ADD THE OFFLOAD MESSAGE
         M$ERRST MSGTEXT1                 OUTPUT THE MESSAGE
         B     OFL8010
         SPACE 3
OFL8500  DS    0H
         CLC   ##SUBCAL(8),OFLEFTSR       INVOKE IKJEFTSR?
         BNE   OFL8540                    NO, BRANCH
         CLI   #SUBHOLD+28+11,1           RC:1                DRK JAN07
         BL    OFL8540                    YES, VALID OFFLOAD
         MVI   MTLEN+4,11
         MVC   INSERT#2(11),MSGBL132      CLEAR AREA
         SR    R15,R15
         ICM   R15,B'0001',#SUBHOLD+28+11                     DRK JAN07
         CVD   R15,DOUBLE
         MVC   INSERT#1(8),##SUBCOM
         MVC   INSERT#2(4),=X'40202120'
         ED    INSERT#2(4),DOUBLE+6
         MVI   INSERT#2,C'='              RETURN CODE
         SPACE 1
         CLI   SUBSECB+3,X'0C'            IKJEFTSR RC=12 (PGM ABEND)?
         BNE   OFL8530                    NO, SKIP MESSAGE
         SPACE 1
         L     R0,#SUBHOLD+28+16        ABEND CODE            DRK NOV07
         SRDL  R0,12                    SHIFT USER ABEND TO R1DRK NOV07
         ST    R0,FULLWORD              SYSTEM ABEND CODE     DRK NOV07
         MVC   INSERT#2+4(7),=C' ABEND='                      DRK NOV07
         UNPK  INSERT#2+11(5),FULLWORD+2(3)                   DRK NOV07
         TR    INSERT#2+11(4),TRTABLE                         DRK NOV07
         MVI   INSERT#2+11,C'S'         S000                  DRK NOV07
         MVI   INSERT#2+11+4,X'40'                            DRK NOV07
         SPACE 1                                              DRK NOV07
         SRL   R1,20                    USER ABEND            DRK NOV07
         CVD   R1,DOUBLE                                      DRK NOV07
         UNPK  DOUBLE(5),DOUBLE+5(3)                          DRK NOV07
         OI    DOUBLE+4,X'F0'           MAKE PRINTABLE        DRK NOV07
         MVI   INSERT#2+11+5,C'U'       U0000                 DRK NOV07
         MVC   INSERT#2+11+6(4),DOUBLE+1 GET USER ABEND       DRK NOV07
         SPACE 1
OFL8530  DS    0H
         M$MSG L171$2
OFL8540  DS    0H
         MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)   CLOSE THE SYSPRINT FILE
         LA    R1,BAMDCB
         FREEPOOL (1)
         SPACE 2
OFL8550  M$ERRST MSGBLANK
         SPACE 1
         LA    R1,=CL8'SYSPRINT'          SYSPRINT DNAME
         BAS   R2,OFL8580                 FREE THIS DATA SET
         LA    R1,SYSUT1                  SYSUT1 DDNAME
         BAS   R2,OFL8580                 FREE THIS DATA SET
         LA    R1,SYSUT2                  SYSUT2 DDNAME
         BAS   R2,OFL8580                 FREE THIS DATA SET
         LA    R1,=CL8'SYSIN'             SYSIN DDNAME
         BAS   R2,OFL8580                 FREE THIS DATA SET
         B     NEWSTAX
         SPACE 1
OFL8580  CLI   0(R1),X'41'                ANY DDNAME ALLOCATED?
         BLR   R2                         NO, RETURN
         MVC   #CMXSYS3+6(8),0(R1)        DDNAME TO FREE
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBUN           UNALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,#CMXSYS3                TEXT POINTER
         USING S99TUNIT,R3
         MVC   S99TUKEY(6),OFLFDDNA       ADD TEXT KEYS
         ST    R3,S99TUPTR                POINT TO TEXT UNIT
         LA    R0,OFLUNALC                UNALLOC EVEN IF "PERM"
         ST    R0,S99TUPTR+4              ADD THIS ELEMENT TOO
         OI    S99TUPTR+4,X'80'           MARK END OF THE TEXT LIST
         SPACE 1
         DYNALLOC
         BR    R2
         SPACE 1
         DROP  R1,R3,R4,R5
         SPACE 2
         B     RESTART0
         SPACE 2
OFFLNULL BR    R14
         SPACE 2
*  OFFLOAD CONSTANTS
         PRINT NOGEN
OFLDCB   DCB   DSORG=PS,DDNAME=OFL,MACRF=(GL,PM),EODAD=OFL8500
OFLDL    EQU   *-OFLDCB
         PRINT NOGEN
OFL484W  DC    C'PDS484W OFFLOAD SHOULD NOT BE INTERRUPTED'
OFL484WL EQU   *-OFL484W
OFL865E  M$TX  'PDS865E THE OFFLOAD OUTPUT CAN NOT BE OPENED'
         PRINT GEN
OFLEFTSR DC    CL8'IKJEFTSR'
OFLPGM   DC    CL8'OFFLOAD'                      OFFLOAD
OFLPARM  DC    AL2(OFLPARML-2)                   PARM LENGTH
OFLPARML EQU   *-OFLPARM
OFLDDNIN DC    X'000100010005',C'SYSIN'          SYSIN    DDNAME
OFLDDNPR DC    X'000100010008',C'SYSPRINT'       SYSPRINT DDNAME
OFLDDNU1 DC    X'000100010006',C'SYSUT1'         SYSUT1   DDNAME
OFLDDNU2 DC    X'000100010006',C'SYSUT2'         SYSUT2   DDNAME
OFLDSNAM DC    X'00020001002C'                   DSNAME
OFLDSMBR DC    X'000300010008'                   MEMBER
OFLALLOC DC    X'00040001000108'                 DISP=SHR
OFLALMOD DC    X'00040001000102'                 DISP=MOD
OFLNORM  DC    X'00050001000108'                 DISP=(,KEEP)
OFLANORM DC    X'00060001000108'                 DISP=(,,KEEP)
OFLDDNAM DC    X'0055000100084040404040404040'   RETURN DDNAME
OFLFDDNA DC    X'000100010008'                   FOR FREE DDNAME
OFLNEW   DC    X'00040001000104'                 DISP=(NEW)
OFLDEL   DC    X'00050001000104'                 DISP=(,DELETE)
OFLCAT   DC    X'00050001000102'                 DISP=(,CATLG)
OFLCDEL  DC    X'00060001000104'                 DISP=(,,DELETE)
OFLPERM  DC    X'00520000'                       PERMANENT ALLOCATION
OFLUNALC DC    X'00070000'                       UNALLOCATE PERMANENT
OFLTRK   DC    X'00070000'                       SPACE=(TRK,
OFLFIRST DC    X'000A00010003',AL3(15)           SPACE=(,15
OFLSECON DC    X'000B00010003',AL3(15)           SPACE=(,,15
OFLOOLD  EQU   X'01'                             DISP=OLD
OFLOMOD  EQU   X'02'                             DISP=MOD
OFLONEW  EQU   X'04'                             DISP=NEW
OFLOSHR  EQU   X'08'                             DISP=SHR
OFLVOLSR DC    X'001000010006404040404040'       VOL=SER=
OFLUNITP DC    X'001500010008',CL8'&PUNIT'       UNIT=SYSALLDA
         SPACE 2
OFLSDS   DC    X'003C00010002',XL2'4000'         DSORG=PS
OFLSRF   DC    X'004900010001',XL1'90'           DCB=RECFM=FB
OFLSLR   DC    X'004200010002',AL2(80)           DCB=LRECL=80
OFLSBL   DC    X'003000010002',AL2(27920)        DCB=BLKSIZE=27920
         SPACE 2
OFLPRTFI DC    X'000A00010003',AL3(050)          SPACE=(,NNN)
OFLPRTSE DC    X'000B00010003',AL3(050)          SPACE=(,(,NNN))
OFLPRTIN DC    X'000900010003',AL3(27930)        SPACE=(27930)
OFLSINSP DC    X'000900010003',AL3(3120)         SPACE=(3120)
         SPACE 1
OFLPRTRE DC    X'004900010001',XL1'C4'           DCB=RECFM=UA
OFLPRTLR DC    X'004200010002',AL2(121)          DCB=LRECL=121
OFLPRTBL DC    X'003000010002',AL2(121)          DCB=BLKSIZE=121
         SPACE 2
OFLSINRE DC    X'004900010001',XL1'90'           DCB=RECFM=FB
OFLSINLR DC    X'004200010002',AL2(80)           DCB=LRECL=80
OFLSINBL DC    X'003000010002',AL2(3120)         DCB=BLKSIZE=3120
         SPACE 2
OFLSYSIN DC    A(OFLPRTFI,OFLPRTSE,OFLSINSP)
         DC    A(OFLSINRE,OFLSINLR,OFLSINBL)
         DC    A(OFLNEW,OFLDEL,OFLCDEL),X'80',AL3(OFLPERM)
OFLSYSPR DC    A(OFLPRTFI,OFLPRTSE,OFLPRTIN)
         DC    A(OFLPRTRE,OFLPRTLR,OFLPRTBL)
         DC    A(OFLNEW,OFLDEL,OFLCDEL),X'80',AL3(OFLPERM)
#OFLASSP EQU   X'80'            OFFLOAD: ASSOCIATES PHASE
#OFLONE  EQU   X'10'            OFFLOAD: A SINGLE MEMBER
#OFLASS2 EQU   X'01'            OFFLOAD: OFFLOAD ASSOCIATES TOO
OFLHDR   DC    CL31' O I=SYSUT1,O=SYSUT2,T=IEBUPDTE'
SYSUT1   DC    CL8'SYSUT1'
SYSUT2   DC    CL8'SYSUT2'
$OFFLOAD DC    CL8'$OFFLOAD'
SELECT   DC    CL5' S M='
         SPACE 3
OBTAIN2  CAMLST SEARCH,0,0,0
         ORG   OBTAIN2+4
         SPACE 3
LOCATE2  CAMLST NAME,0,,0
         ORG   LOCATE2+4
         SPACE 3
