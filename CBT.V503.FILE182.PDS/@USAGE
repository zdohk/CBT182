         TITLE 'P D S  --  PDS USAGE                         09/28/88'
***********************************************************************
***      USAGE SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
USAGE    CSECT
         USING *,R8
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?           DRK SEP10
         BO    US010                  YES                     DRK SEP10
*** REOPEN THE DATA SET TO ENSURE DS1LSTAR IS CORRECT
         OI    FLAGSJJ,FNOREAD         CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DS1LSTAR     SET BEYOND END OF DATA SET
         L     R15,=V(EXCP)
         BASR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD      CALL IS COMPLETED
         SPACE 2
US010    DS    0H
         L     R15,=A(DSNAMES)
         BASR  R2,R15                   FORMAT THE ALLOCATION STATUS
         SPACE 1
         MVI   INSERT#2+20,0           ASSUME NO DATA MOVER
         CLI   DS1DSNAM+70,X'FF'       ANY ASM2 IDENTIFIER?
         BNE   *+8                     NO, BRANCH
         MVI   INSERT#2+20,C'A'        YES, ASSUME ASM2
         CLI   DS1DSNAM+104,0          ANY FDR/ABR FLAGS?
         BZ    *+8                     NO, BRANCH
         MVI   INSERT#2+20,C'F'        YES, ASSUME FDR/ABR
         TM    DS1DSNAM+103,X'F0'      ANY DMS/OS IDENTIFIER?
         BNO   *+8                     NO, BRANCH
         MVI   INSERT#2+20,C'D'        YES, ASSUME DMS/OS
*
         MVC   INSERT#1(66),MSG180H
         MVI   MTLEN,56
         CLI   INSERT#2+20,0           ANY DATA MOVER?
         BE    *+8                     NO, BRANCH
         MVI   MTLEN,66                YES, USE 66 CHARACTERS
*
         CLI   INSERT#2+20,C'D'        DMS/OS
         BNE   *+10                    NO, BRANCH
         MVC   INSERT#1+47(19),MSG180D YES, USE DMS/OS
         CLI   INSERT#2+20,C'F'        FDR/ABR?
         BNE   *+10                    NO, BRANCH
         MVC   INSERT#1+47(19),MSG180F YES, USE FDR/ABR
*
         M$MSG L180$1
         MVC   INSERT#1(66),BLANK128
         SR    R15,R15
         MVC   INSERT#1+10(7),USAL000
         ICM   R15,B'0111',DS1CREDT    DATA SET CREATION DATE NULL?
         BZ    US070                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAS   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+08(9),USAL4020
         ED    INSERT#1+08(9),MSGTEXT1
         AIF   ('&ISODT'  NE 'YES').NOISOU1                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1+08+1   MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1+08+1(8),DOUBLE   YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOU1 ANOP                                                 DRK NOV01
         SPACE 1
US070    SR    R15,R15
         MVC   INSERT#1+19(7),USAL000
         ICM   R15,B'0111',DS1EXPDT    DATA SET EXPIRATION DATE NULL?
         BZ    US080                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAS   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+17(9),USAL4020
         ED    INSERT#1+17(9),MSGTEXT1
         AIF   ('&ISODT'  NE 'YES').NOISOU2                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1+17+1   MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1+17+1(8),DOUBLE   YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOU2 ANOP                                                 DRK NOV01
         SPACE 1
US080    SR    R15,R15
         MVC   INSERT#1+29(7),USAL000
         ICM   R15,B'0111',DS1REFD     LAST OPEN DATE NULL?
         BZ    US090                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAS   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+27(9),USAL4020
         ED    INSERT#1+27(9),MSGTEXT1
         AIF   ('&ISODT'  NE 'YES').NOISOU3                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1+27+1   MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1+27+1(8),DOUBLE   YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOU3 ANOP                                                 DRK NOV01
         SPACE 1
US090    MVC   INSERT#1+38(3),USALYES  ASSUME UPDATED
         TM    DS1DSIND,DS1DSCHA       CORRECT?
         BO    *+10                    YES, BRANCH
         MVC   INSERT#1+38(3),USALNO   NO, RESET THE DATA
         SPACE 1
         MVC   INSERT#1+47(4),=C'NONE' ASSUME NO SECURITY
         TM    DS1DSIND,X'40'+X'10'+X'04'  CORRECT?
         BZ    US100                   YES, BRANCH
         MVC   INSERT#1+47(4),USALRACF ASSUME RACF DEFINED
         TM    DS1DSIND,X'40'          CORRECT?
         BO    US100                   YES, BRANCH
         MVC   INSERT#1+47(4),USALREAD ASSUME READ PROTECTED
         TM    DS1DSIND,X'10'+X'04'    CORRECT?
         BM    US100                   YES, BRANCH
         MVC   INSERT#1+47(5),USALWRIT   MUST BE WRITE PROTECTED
         SPACE 1
US100    MVC   INSERT#1+57(8),DS1SYSCD USERID IF ANY
         CLI   INSERT#2+20,0           ANY DATA MOVER?
         BE    US118                   NO, BRANCH
         CLI   INSERT#2+20,C'F'        FDR/ABR DATA MOVER?
         BE    US108                   YES, BRANCH
         SR    R15,R15
         MVI   INSERT#1+38,X'40'
         MVC   INSERT#1+39(7),USAL000
         CLI   INSERT#2+20,C'D'        DMS/OS                 DRK OCT04
         BNE   US101                   NO, BRANCH             DRK OCT04
         ICM   R15,B'0111',DS1SYSCD+8  LAST CHANGE DATE NULL? DRK OCT04
         BZ    US104                   YES, BRANCH            DRK OCT04
         B     US102                                          DRK OCT04
US101    DS    0H                                             DRK OCT04
         ICM   R15,B'0111',DS1DSSN+3   LAST CHANGE DATE NULL?
         BZ    US104                   YES, BRANCH
US102    DS    0H                                             DRK OCT04
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAS   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+37(9),USAL4020
         ED    INSERT#1+37(9),MSGTEXT1
         AIF   ('&ISODT'  NE 'YES').NOISOU4                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1+37+1   MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1+37+1(8),DOUBLE   YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOU4 ANOP                                                 DRK NOV01
         SPACE 1                                                    ABL
US104    CLI   INSERT#2+20,C'A'        ASM2 DATA MOVER?
         BE    US118                   YES, BRANCH
         MVC   INSERT#1+47(8),DS1SYSCD   DMS/OS LAST USER
*                                                             DRK OCT04
*        MVC   INSERT#1+56(5),DS1SYSCD+8 DMS/OS FIRST OPENER PART 1
*        MVC   INSERT#1+61(3),DS1DSSN    DMS/OS FIRST OPENER PART 2
         B     US118
         SPACE 1                                                    ABL
US108    DS    0H                        FDR/ABR DATA
         MVC   INSERT#1+47(19),BLANK128
         SR    R1,R1
         IC    R1,DS1DSNAM+103           CURRENT BACKUP
         CVD   R1,DOUBLE
         MVC   INSERT#1+48(4),=X'40202120'
         ED    INSERT#1+48(4),DOUBLE+6
         TM    DS1DSNAM+104,X'01'        CURRENT BACKUP EXISTS?
         BO    *+10                      YES, BRANCH
         MVC   INSERT#1+48(4),=C'NONE'   NO, CHANGE TO NONE
         MVC   INSERT#1+54(3),USALYES    ASSUME OLD BACKUP EXISTS
         TM    DS1DSNAM+104,X'02'        OLD BACKUP EXISTS?
         BO    *+10                      YES, BRANCH
         MVC   INSERT#1+54(3),USALNO     NO, CHANGE TO NO
         SPACE 1                                                    ABL
         TM    DS1DSNAM+104,X'10'        ARCHIVE REQUESTED?
         BNO   *+10                      NO, BRANCH
         MVC   INSERT#1+59(6),USALARCH   YES, NOTE IT
         TM    DS1DSNAM+104,X'40'        ALWAYS REQUESTED?
         BNO   *+10                      NO, BRANCH
         MVC   INSERT#1+59(6),USALALWA   YES, NOTE IT
         TM    DS1DSNAM+104,X'80'        NOARCH REQUESTED?
         BNO   *+10                      NO, BRANCH
         MVC   INSERT#1+59(6),USALNOAR   YES, NOTE IT
         TM    DS1DSNAM+104,X'C0'        NOABR REQUESTED?
         BNO   *+10                      NO, BRANCH
         MVC   INSERT#1+59(6),USALNOAB   YES, NOTE IT
         SPACE 1                                                    ABL
US118    DS    0H
         M$MSG L180$1
         M$ERRST MSGBLANK
         SPACE 2
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
         SR    R15,R15
*
         ICM   R15,3,32+14(R1) <----+   NUMBER OF TRACKS IN EXTENT
         AR    R0,R15               |   ADD NUMBER OF TRACKS
         LA    R1,16(,R1)           |   NEXT EXTENT
         BCT   R14,*-4-2-4  --------+   BRANCH FOR ALL EXTENTS
*
         ST    R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         SR    R1,R1
         ICM   R1,3,DS1LSTAR            GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         ST    R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 2
         MVC   INSERT#1(120),BLANK128
         MVI   MTLEN,120
         L     R4,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R3,NUMEXT               NUMBER OF EXTENTS
         LA    R2,INSERT#1-2           POSITION FOR FIRST DATA
         SPACE 1
US120    LH    R1,32+14(,R4)           TRACKS IN THIS EXTENT
         N     R1,=XL4'0000FFFF'       MASK OFF SIGN BITS
         CVD   R1,DOUBLE
         MVC   MSGTEXT1(6),=X'402020202120'
         ED    MSGTEXT1(6),DOUBLE+5
         LA    R1,MSGTEXT1-1
         LA    R5,6
         SPACE 1
US130    LA    R1,1(,R1)               SCAN
         BCTR  R5,0                        FOR
         CLI   0(R1),X'40'                    FIRST
         BE    US130                               NON-BLANK
         SPACE 1
         MVI   0(R2),C','
         MVC   2(*-*,R2),0(R1)        <<EXECUTED>>
         EX    R5,*-6                 MOVE IN THE LENGTH TEXT
         LA    R2,2+1(R2,R5)          POSITION FOR NEXT COMMA
         LA    R4,16(,R4)             NEXT EXTENT START
         BCT   R3,US120               DO ALL EXTENTS
         SPACE 1
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?           DRK AUG05
         BNO   US135                  NO                      DRK AUG05
         MVC   INSERT#1(3),=C'N/A'    YES, MARK N/A           DRK AUG05
US135    DS    0H                                             DRK AUG05
         M$MSG L181$1
         M$ERRST MSGBLANK
         SPACE 2
US200    MVI   MTLEN,42
         MVC   INSERT#1(42),MSG182H
         M$MSG L182$1
         MVC   INSERT#1(42),MSG182E
         SPACE 2
         L     R1,DSNTOTAL            TOTAL DATA SET TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+9(8),DOUBLE+4                         DRK MAY98
         SPACE 1
         L     R1,DSNTOTAL            TOTAL DATA SET TRACKS
         S     R1,DSNEMPTY            TOTAL DATA SET USED TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+17(8),DOUBLE+4                        DRK MAY98
         SPACE 1
         L     R1,DSNEMPTY            TOTAL DATA SET FREE TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+25(8),DOUBLE+4                        DRK MAY98
         SPACE 1
         LH    R1,NUMEXT              TOTAL DATA SET EXTENTS
         CVD   R1,DOUBLE
         ED    INSERT#1+38(4),DOUBLE+6
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?           DRK AUG05
         BNO   US210                  NO                      DRK AUG05
         MVC   INSERT#1+09(8),=C'     N/A'  YES, MARK N/A     DRK AUG05
         MVC   INSERT#1+17(8),=C'     N/A'                    DRK AUG05
         MVC   INSERT#1+25(8),=C'     N/A'                    DRK AUG05
         SR    R1,R1                                          DRK AUG05
         IC    R1,DS1NOEPV                                    DRK AUG05
         CVD   R1,DOUBLE                                      DRK AUG05
         MVC   INSERT#1+38(4),=X'40202120'                    DRK AUG05
         ED    INSERT#1+38(4),DOUBLE+6                        DRK AUG05
         SPACE 1
US210    DS    0H
         M$MSG L182$1
         M$ERRST MSGBLANK
         SPACE 2
         TM    DSORG,DS1DSGPO         DSORG=PO?
         BNO   US250                  NO, BRANCH
         MVI   STARTTR+2,X'01'        TTR=000001 (START OF DIRECTORY)
         SPACE 1
         BAS   R14,READDIR            SCAN THROUGH THE DIRECTORY
         SPACE 1
         MVI   MTLEN,64
         MVC   INSERT#1(64),MSG183H
         M$MSG L183$1
         MVC   INSERT#1(64),MSG183E
         SPACE 1
         L     R1,TOTBLOCK            TOTAL DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+09(8),DOUBLE+4
         SPACE 1
         L     R1,TOTUSED             TOTAL USED DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+17(8),DOUBLE+4
         SPACE 1
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?           DRK MAR07
         BO    US215                  YES                     DRK MAR07
         L     R1,TOTBLOCK            TOTAL DIRECTORY BLOCKS
         S     R1,TOTUSED             TOTAL FREE DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+25(8),DOUBLE+4
         B     US218                                          DRK MAR07
US215    DS    0H                                             DRK MAR07
         MVC   INSERT#1+25(8),BLANKS                          DRK MAR07
         MVC   INSERT#1+25+5(3),=C'N/A' MARK N/A              DRK MAR07
US218    DS    0H                                             DRK MAR07
         SPACE 1
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?           DRK FEB07
         BO    US220                  YES                     DRK FEB07
         L     R1,TOTTRKS             TOTAL TRACKS FOR DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+34(8),DOUBLE+4
         B     US230                                          DRK FEB07
         SPACE 1
US220    DS    0H                                             DRK FEB07
         MVC   INSERT#1+34(8),BLANKS                          DRK FEB07
         MVC   INSERT#1+34+5(3),=C'N/A' MARK N/A              DRK MAR07
US230    DS    0H                                             DRK FEB07
         L     R1,TOTMEMR             TOTAL REAL ENTRIES
         A     R1,TOTMEMA             TOTAL ALIAS ENTRIES
         CVD   R1,DOUBLE
         ED    INSERT#1+43(8),DOUBLE+4
         SPACE 1
         L     R1,TOTMEMA             TOTAL ALIAS ENTRIES
         CVD   R1,DOUBLE
         ED    INSERT#1+52(8),DOUBLE+4
         SPACE 1
         M$MSG L183$1
         M$ERRST MSGBLANK
         SPACE 2
US250    DS    0H
         AIF   ('&MVSLEV' LE 'MVS041').NOCSI                  DRK NOV02
         TM    DS1SMSFG,DS1SMSDS    SMS MANAGED?              DRK NOV02
         BNO   US300                NO, DONE                  DRK NOV02
         LA    R2,FIRST4K           BASE FOR SECOND4K         DRK NOV02
         LA    R1,CSIRSN-@4K(,R2)   -> REASON AREA            DRK NOV02
         ST    R1,PARMLIST+0        SAVE ADDRESS IN PARM      DRK NOV02
         LA    R1,CSIFIELD-@4K(,R2) -> SELECT CRITERIA        DRK NOV02
         ST    R1,PARMLIST+4        SAVE ADDRESS IN PARM      DRK NOV02
         LA    R1,CSIWORK-@4K(,R2)  -> WORK AREA              DRK NOV02
         ST    R1,PARMLIST+8        SAVE ADDRESS IN PARM      DRK NOV02
         OI    PARMLIST+8,X'80'     TURN ON LAST PARM BIT     DRK NOV02
         SPACE 1                                              DRK NOV02
         MVI   CSIFIELD-@4K(R2),C' ' BLANK CSIFIELD           DRK NOV02
         MVC   CSIFIELD-@4K+1(177,R2),CSIFIELD-@4K(R2)        DRK NOV02
         L     R1,=F'1024'          LENGTH FOR CSIWORK (1K)   DRK NOV02
         ST    R1,CAIUSRLN-@4K(R2)  SAVE LEN IN 1ST WORD      DRK NOV02
         L     R1,=F'3'             SET 3 ENTRIES             DRK NOV02
         STCM  R1,B'0011',CSINUMEN-@4K(R2) NUMBER OF ENTRIES  DRK NOV02
         MVC   CSIFLDNM-@4K(24,R2),CSIFLD1 FIELD NAMES        DRK NOV02
         MVI   CSISTYPS-@4K(R2),C'A'       NON-VSAM ONLY      DRK NOV02
         MVI   CSIS1CAT-@4K(R2),C'Y'       SEARCH 1 CATALOG   DRK NOV02
         MVC   CSIFILTK-@4K(44,R2),DSNAME  MOVE DSNAME        DRK NOV02
         SPACE 1                                              DRK NOV02
         LA    R1,PARMLIST          POINT TO PARMS FOR CSI    DRK NOV02
         LINK  EP=IGGCSI00          CSI:ST.LOUIS              DRK NOV02
         LTR   R15,R15              SUCCESSFUL?               DRK NOV02
         BNZ   US300                NO, DONE                  DRK NOV02
         SPACE 1                                              DRK NOV02
         MVI   MTLEN,46             LENGTH OF MESSAGE         DRK NOV02
         MVC   INSERT#1(46),MSG191H HEADER                    DRK NOV02
         M$MSG L191$1                                         DRK NOV02
         MVC   INSERT#1(46),MSG191S SMS DATA                  DRK NOV02
         LA    R3,CSIEFLAG-@4K(,R2) POINT TO ENTRY            DRK NOV02
         SR    R1,R1                CLEAR REG                 DRK NOV02
         LA    R3,46+4+2*3(,R3)     START OF 1ST VL FIELD     DRK NOV02
         ICM   R1,B'0011',0(R3)     LENGTH OF STORCLAS        DRK NOV02
         BZ    USNOSTOR                                       DRK NOV02
         BCTR  R1,0                 MACHINE STRING LENGTH     DRK NOV02
         MVC   INSERT#1+00(*-*),2(R3) MOVE STORCLAS           DRK NOV02
         EX    R1,*-6               <<EXECUTED>>              DRK NOV02
         LA    R1,1(,R1)            ACTUAL LENGTH             DRK NOV02
USNOSTOR DS    0H                                             DRK NOV02
         LA    R3,2(R3,R1)          START OF 2ND VL FIELD     DRK NOV02
         ICM   R1,B'0011',0(R3)     LENGTH OF MGMTCLAS        DRK NOV02
         BZ    USNOMGMT                                       DRK NOV02
         BCTR  R1,0                 MACHINE STRING LENGTH     DRK NOV02
         MVC   INSERT#1+15(*-*),2(R3) MOVE MGMTCLAS           DRK NOV02
         EX    R1,*-6               <<EXECUTED>>              DRK NOV02
         LA    R1,1(,R1)            ACTUAL LENGTH             DRK NOV02
USNOMGMT DS    0H                                             DRK NOV02
         LA    R3,2(R3,R1)          START OF 3RD VL FIELD     DRK NOV02
         ICM   R1,B'0011',0(R3)     LENGTH OF DATACLAS        DRK NOV02
         BZ    USNODATA                                       DRK NOV02
         BCTR  R1,0                 MACHINE STRING LENGTH     DRK NOV02
         MVC   INSERT#1+27(*-*),2(R3) MOVE DATACLAS           DRK NOV02
         EX    R1,*-6               <<EXECUTED>>              DRK NOV02
USNODATA DS    0H                                             DRK NOV02
         TM    DS1DSORG,DS1DSGPO           PDS DATASET?       DRK NOV02
         BNO   *+4+6                       NO                 DRK NOV02
         MVC   INSERT#1+39(8),=CL8'PDS'                       DRK NOV02
         TM    DS1SMSFG,DS1PDSE            PDSE DATASET?      DRK NOV02
         BNO   *+4+6                       NO                 DRK NOV02
         MVC   INSERT#1+39(8),=CL8'LIBRARY'                   DRK NOV02
         M$MSG L191$1                                         DRK NOV02
         M$ERRST MSGBLANK                                     DRK NOV02
         SPACE 2                                              DRK NOV02
         B     US300                                          DRK NOV02
CSIFLD1  DC    CL8'STORCLAS'                                  DRK NOV02
CSIFLD2  DC    CL8'MGMTCLAS'                                  DRK NOV02
CSIFLD3  DC    CL8'DATACLAS'                                  DRK NOV02
.NOCSI   ANOP                                                 DRK NOV02
         SPACE 1
US300    DS    0H
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?           DRK SEP10
         BNO   US300C                 NO, CONTINUE            DRK SEP10
         L     R4,=A(FAMS)            FAMS SUBROUTINE         DRK SEP10
         BASR  R3,R4                  CALL SUBROUTINE         DRK SEP10
         B     US300C                                         DRK SEP10
*
*        ISPF DSINFO CALL REPLACE BY FAMS CALL                DRK SEP10
*
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?           DRK AUG05
         BNO   US300C                 NO, CONTINUE            DRK FEB07
         TM    SPFLAG0,SPFDON         ISPMODE ACTIVE?         DRK FEB07
         BNO   US300C                 NO, BRANCH              DRK FEB07
         LA    R14,DSINFO             DSINFO                  DRK FEB07
         ST    R14,PARMLIST           1ST PARM                DRK FEB07
         LA    R15,#USADSN                                    DRK FEB07
         MVI   0(R15),C''''           START QUOTE             DRK FEB07
         LA    R15,1(R15)                                     DRK FEB07
         MVC   0(44,R15),DSNAME       DSNAME                  DRK FEB07
         AH    R15,DSNLEN                                     DRK FEB07
         MVI   0(R15),C''''           END QUOTE               DRK FEB07
         LA    R15,#USADSN            DSN                     DRK FEB07
         ST    R15,PARMLIST+4         2ND PARM                DRK FEB07
         OI    PARMLIST+4,X'80'       MARK END OF PARM LIST   DRK FEB07
         LA    R1,PARMLIST            PT TO PARMLIST          DRK FEB07
         L     R15,ISPLINK                                    DRK FEB07
         BASR  R14,R15                CALL ISPF               DRK FEB07
         LTR   R15,R15                NORMAL COMPLETION?      DRK FEB07
         BNZ   US300C                 NO                      DRK FEB07
         SPACE 1
         LA    R14,VCOPY              VCOPY                   DRK FEB07
         ST    R14,PARMLIST           1ST PARM                DRK FEB07
         LA    R14,ZPAGE              GET PAGES USED, PCT     DRK FEB07
         MVC   DOUBLE(8),VARLEN       MOVE LENGTH ARRAY       DRK OCT07
         LA    R15,DOUBLE             LENGTH OF VARIABLES     DRK OCT07
         LA    R0,PAGEUSED            RETURN AREA             DRK FEB07
         LA    R1,ISPMOVE             MOVE                    DRK FEB07
         STM   R14,R1,PARMLIST+4      2ND-5TH PARMS           DRK FEB07
         OI    PARMLIST+16,X'80'      MARK END OF PARM LIST   DRK FEB07
         LA    R1,PARMLIST            PT TO PARMLIST          DRK FEB07
         L     R15,ISPLINK                                    DRK FEB07
         BASR  R14,R15                CALL ISPF               DRK FEB07
         LTR   R15,R15                NORMAL COMPLETION?      DRK FEB07
         BNZ   US300C                 NO                      DRK FEB07
         MVI   MTLEN,39               YES                     DRK FEB07
         MVC   INSERT#1(39),MSG184H2                          DRK FEB07
         M$MSG L184$1                                         DRK FEB07
         MVC   INSERT#1(39),BLANK128                          DRK FEB07
         MVC   INSERT#1+12(13),PAGEUSED     ZDSPAGU           DRK FEB07
         MVC   INSERT#1+36(3),PAGEUSED+23   ZDSPERU           DRK FEB07
         M$MSG L184$1                                         DRK FEB07
         B     US300C                 SKIP EXTENT MESSAGES    DRK FEB07
VARLEN   DC    F'13',F'13'            LENGTH OF VARS          DRK FEB07
ISPMOVE  DC    CL4'MOVE'              ACTION                  DRK FEB07
VCOPY    DC    CL8'VCOPY'             SERVICE                 DRK FEB07
ZPAGE    DC    C'(ZDSPAGU ZDSPERU)'   VARIABLE NAMES          DRK FEB07
DSINFO   DC    CL8'DSINFO'            SERVICE                 DRK FEB07
         SPACE 1
US300C   DS    0H
         TM    FLAGSHH,FOPTIONS       ANY OPTION?
         BNO   NEWCMD                 NO, BRANCH
         TM    DS1SMSFG,DS1PDSE       PDSE DATASET?
         BO    US500                  YES
         MVI   MTLEN,70
         MVC   INSERT#1(70),MSG184H
         M$MSG L184$1
         MVC   INSERT#1(70),MSG184U
         M$MSG L184$1
         MVC   INSERT#1(70),BLANK128
         L     R2,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R3,NUMEXT
         SR    R4,R4                  EXTENT NUMBER
         SR    R5,R5                  BEGINNING TT OF TTR
         SPACE 1
US310    CVD   R4,DOUBLE              EXTENT
         A     R4,=F'1'
         MVC   INSERT#1+2(4),=X'40202120'
         ED    INSERT#1+2(4),DOUBLE+6
         L     R1,32(,R2)             UCB ADDRESS
         AIF   ('&MVSLEV' GE 'MVS510').MVS51A                 DRK MAY98
         MVI   INSERT#1+8,C'0'        ZERO FILL               DRK JUN98
         MVC   INSERT#1+9(3),13(R1)
         AGO   .MVS51AX                                       DRK MAY98
.MVS51A  ANOP                                                 DRK MAY98
         LA    R1,0(,R1)              CLEAR HIGH-ORDER BYTE   DRK MAY98
         ST    R1,UCBADDR                                     DRK MAY98
         UCBDEVN UCBPTR=UCBADDR,DEVN=INSERT#1+8  4-DIGIT DEVN DRK MAY98
         CLI   INSERT#1+8,C' '        3DIGIT UCB NAME?        DRK JUN98
         BNE   *+8                    NO                      DRK JUN98
         MVI   INSERT#1+8,C'0'        YES, THEN ZERO FILL     DRK JUN98
.MVS51AX ANOP                                                 DRK MAY98
         LH    R6,32+14(,R2)          TRACKS IN THIS EXTENT
         N     R6,=XL4'0000FFFF'      MASK OFF SIGN BITS
         LR    R0,R5
         AR    R5,R6                  START OF NEXT EXTENT
         LA    R15,2                  LOOPS
         LA    R14,INSERT#1+16
         SPACE 1
US320    SRDL  R0,4                   TT OF TTR
         SRL   R1,28                  LOW REGISTER POSITION
         MVI   2(R14),C'.'
         IC    R1,HEXTBL(R1)
         STC   R1,1(,R14)
         SRDL  R0,4
         SRL   R1,28
         IC    R1,HEXTBL(R1)
         STC   R1,0(,R14)
         SH    R14,=H'3'
         BCT   R15,US320
         SPACE 1
         CLI   INSERT#1+24,C'.'       SECOND TTR VALUE?
         BE    US330                  YES, BRANCH
         LA    R14,INSERT#1+22
         LA    R15,2
         LTR   R0,R5                  ANY DATA?
         BZ    US320                  NO, BRANCH
         BCTR  R0,0                   YES, REDUCE BY ONE
         B     US320
         SPACE 1
US330    MVI   INSERT#1+18,X'40'      CLEAR FIRST EXTRA PERIOD
         MVI   INSERT#1+24,X'40'      CLEAR SECOND EXTRA PERIOD
         CVD   R6,DOUBLE              TRACKS
         MVC   INSERT#1+26(6),=X'402020202120'
         ED    INSERT#1+26(6),DOUBLE+5
         SPACE 2
         ICM   R0,B'1111',38(R2)      FIRST DEVICE CC.CC.HH.HH
         LA    R15,4                  LOOPS
         LA    R14,INSERT#1+43
         SPACE 1
US420    SRDL  R0,4                   TT OF TTR
         SRL   R1,28                  LOW REGISTER POSITION
         MVI   2(R14),C'.'
         IC    R1,HEXTBL(R1)
         STC   R1,1(,R14)
         SRDL  R0,4
         SRL   R1,28
         IC    R1,HEXTBL(R1)
         STC   R1,0(,R14)
         SH    R14,=H'3'
         BCT   R15,US420
         SPACE 1
         CLI   INSERT#1+57,C'.'       SECOND TTR VALUE?
         BE    US430                  YES, BRANCH
         LA    R14,INSERT#1+55
         ICM   R0,B'1111',42(R2)      SECOND CC.CC.HH.HH FIGURE
         LA    R15,4
         B     US420
         SPACE 1
US430    MVI   INSERT#1+45,X'40'
         MVI   INSERT#1+57,X'40'
         MVC   INSERT#1+59(3),=C'CYL' ASSUME CYLINDER BOUNDARY
         CLC   40(2,R2),ZERO          FIRST HH.HH=0?
         BE    *+10                   YES, BRANCH
         MVC   INSERT#1+59(3),USALTRK NO, TRACK BOUNDARY
         LH    R1,44(,R2)
         LA    R1,1(,R1)
         CH    R1,DEVTTRKS            END HH.HH=MAX/DEVICE
         BE    *+10                   YES, BRANCH
         MVC   INSERT#1+59(3),USALTRK NO, TRACK BOUNDARY
         SPACE 1
         M$MSG L184$1
         LA    R2,16(,R2)             NEXT EXTENT
         BCT   R3,US310               DO ALL EXTENTS
         SPACE 2
         M$ERRST MSGBLANK
         B     US500
         SPACE 3
MSG180H  DC    CL66'DATA SET: CREATED  EXPIRES  LAST USE  UPDATED  SECUX
               RITY  ASM2ID'
MSG180D  DC    CL19'LASTUSE  FIRSTUSE  '
MSG180F  DC    CL19'BACK#  OLD  ABRFLAG'
MSG182H  DC    CL42'TRACKS: ALLOCATED    USED    FREE  EXTENTS'
MSG182E  DC    CL42'                                          '
         ORG   MSG182E+9
         DC    X'4020202020202120'                            DRK MAY98
         ORG   MSG182E+17
         DC    X'4020202020202120'                            DRK MAY98
         ORG   MSG182E+25
         DC    X'4020202020202120'                            DRK MAY98
         ORG   MSG182E+38
         DC    X'40202120'
         ORG   ,
MSG183H  DC    CL64'DIRECTORY: BLOCKS    USED    FREE   TRACKS  MEMBERSX
                 ALIASES'
MSG183E  DC    CL64'                                            '
         ORG   MSG183E+09
         DC    X'4020202020202120'
         ORG   MSG183E+17
         DC    X'4020202020202120'
         ORG   MSG183E+25
         DC    X'4020202020202120'
         ORG   MSG183E+34
         DC    X'4020202020202120'
         ORG   MSG183E+43
         DC    X'4020202020202120'
         ORG   MSG183E+52
         DC    X'4020202020202120'
         ORG   ,
MSG184H  DC    CL70'EXTENT  UCB  LO TT-HI TT  TRACKS     LOW CCHH-HIGH X
               CCHH    BOUNDARY '
MSG184U  DC    CL70'------  ---- ----- -----  ------  ----------- -----X
               ------  -------- '
MSG191H  DC    CL46'STORAGE CLASS  MGMT CLASS  DATA CLASS  DSNTYPE'
MSG191S  DC    CL46'                                              '
         SPACE 1
MSG184H2 DC    CL39'PDSE SPACE:    USED PAGES     %UTILIZED'  DRK FEB07
*                               1234567890123 1234567890123   DRK FEB07
         SPACE 1
USAL000  DC    C'0/00/00'
USAL4020 DC    X'402021612020612020'
USALYES  DC    C'YES'
USALNO   DC    C'NO '
USALARCH DC    C'ARCHIV'
USALALWA DC    C'ALWAYS'
USALNOAR DC    C'NOARCH'
USALNOAB DC    C'NOABR '
USALTRK  DC    C'TRK'
USALRACF DC    C'RACF'
USALREAD DC    C'READ'
USALWRIT DC    C'WRITE'
         SPACE 3
OBTAIN   CAMLST SEARCH,0,0,0
         ORG   OBTAIN+4
         SPACE 3
LOCATE   CAMLST NAME,0,,0
         ORG   LOCATE+4
         PRINT GEN
*                                      .
US500    BASR  R8,0                     OUTPUT ADDITIONAL DSCB DATA
         USING *,R8
         M$ERRST MSGBLANK
*                                      .
         MVI   MTLEN,70
         MVC   INSERT#1(70),MSG185T
         M$MSG L185$1
*                                      .
         MVC   INSERT#1(70),MSG185H1
         M$MSG L185$1
*                                      .
         MVC   INSERT#1(70),IECSDSL1+00  IN CHARACTER MODE
         MVC   INSERT#1(44),DSNAME       OVERLAY DSNAME IN POSITION
         TR    INSERT#1(70),TRLINE       TRANSLATE TO PRINTABLE
         M$MSG L185$1
*                                      .
         MVC   INSERT#1(70),IECSDSL1+00  IN CHARACTER MODE
         MVC   INSERT#1(44),DSNAME       OVERLAY DSNAME IN POSITION
         TR    INSERT#1(70),TOPLINE      CONVERT 11, 12,... TO F1
         M$MSG L185$1
*                                      .
         MVC   INSERT#1(70),IECSDSL1+00  IN CHARACTER MODE
         MVC   INSERT#1(44),DSNAME       OVERLAY DSNAME IN POSITION
         TR    INSERT#1(70),BOTLINE      CONVERT 13, 23,... TO F3
         M$MSG L185$1
*                                      .
         M$ERRST MSGBLANK
*                                      .
         MVC   INSERT#1(70),MSG185H2
         M$MSG L185$1
*                                      .
         MVC   INSERT#1(70),IECSDSL1+70  IN CHARACTER MODE
         TR    INSERT#1(70),TRLINE       TRANSLATE TO PRINTABLE
         M$MSG L185$1
*                                      .
         MVC   INSERT#1(70),IECSDSL1+70  IN CHARACTER MODE
         TR    INSERT#1(70),TOPLINE      CONVERT 11, 12,... TO F1
         M$MSG L185$1
*                                      .
         MVC   INSERT#1(70),IECSDSL1+70  IN CHARACTER MODE
         TR    INSERT#1(70),BOTLINE      CONVERT 13, 23,... TO F3
         M$MSG L185$1
*                                      .
         M$ERRST MSGBLANK
         M$ERRST MSGBLANK               OUTPUT ADDITIONAL DIR DATA
         MVC   INSERT#1(100),BLANK128   CLEAR THE HEADER LINE
         MVC   INSERT#1(40),MSG185H     HEADER INFORMATION
         M$MSG L186$1
         MVC   INSERT#1(40),MSG185U     UNDERLINE INFORMATION
         SPACE 2
         SR    R2,R2                    OFFSET IN THE DIRECTORY
         LA    R3,USAHDR                START OF HEADER NAMES
         BAS   R4,USAFMT10              OUTPUT FIRST MESSAGE
         SPACE 1
         MVC   INSERT#1+13(44),DSNAME   ADD THE DATA SET NAME
         BAS   R4,USAFMT                (00)
         LA    R2,44(,R2)               POSITION PAST DSNAME
         SPACE 1
         LA    R0,1                     1 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT                (2C)
         LA    R2,1(,R2)                POSITION PAST DS1FMTID
         SPACE 1
         MVC   INSERT#1+13(6),DS1DSSN   ADD THE VOLUME NAME
         NC    INSERT#1+13(6),USALCCCC  MASK OUT FOLLOWING BITS
         CLC   INSERT#1+13(6),USALCCCC  SAME?
         MVC   INSERT#1+13(6),DS1DSSN
         BE    US550                    YES, BRANCH
         LA    R0,6                     6 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
US550    BAS   R4,USAFMT
         LA    R2,6(,R2)                POSITION PAST VOLUME
         SPACE 1
         LH    R0,DS1DSNAM(R2)
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,2(,R2)                SKIP DS1VOLSQ
         SPACE 1
         LA    R0,3                     3 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,3(,R2)                POSITION PAST DS1CREDT
         SPACE 1
         LA    R0,3                     3 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,3(,R2)                POSITION PAST DS1EXPDT
         SPACE 1
         SR    R0,R0
         IC    R0,DS1DSNAM(R2)
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,1(,R2)                SKIP DS1NOEPV
         SPACE 1
         SR    R0,R0
         IC    R0,DS1DSNAM(R2)
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,1(,R2)                SKIP DS1NOBDB
         SPACE 1
         LA    R0,1                     1 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,1(,R2)                POSITION PAST DS1FLAG1DRK SEP99
         SPACE 1
         MVC   INSERT#1+13(13),DS1SYSCD ADD THE SYSTEM CODE
         NC    INSERT#1+13(13),USALCCCC MASK OUT FOLLOWING BITS
         CLC   INSERT#1+13(13),USALCCCC SAME?
         MVC   INSERT#1+13(13),DS1SYSCD
         BE    US560                    YES, BRANCH
         MVC   INSERT#1+13(50),BLANK128 CLEAR THE DESCRIPTION
         LA    R0,13                    13 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
US560    BAS   R4,USAFMT
         LA    R2,13(,R2)               POSITION PAST SYSTEM CODE
         SPACE 1
         LA    R0,3                     3 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,3(,R2)                POSITION PAST DS1REFD
         SPACE 1
         LA    R0,1                     1 INPUT BYTES         DRK SEP99
         BAS   R4,USAHEX                CONVERT TO HEX        DRK SEP99
         BAS   R4,USAFMT                                      DRK SEP99
         LA    R2,1(,R2)                POSITION PAST DS1SMSFGDRK SEP99
         SPACE 1                                              DRK SEP99
         LA    R0,3                     3 INPUT BYTES         DRK SEP99
         BAS   R4,USAHEX                CONVERT TO HEX        DRK SEP99
         BAS   R4,USAFMT                                      DRK SEP99
         LA    R2,3(,R2)                POSITION PAST DS1SCEXTDRK SEP99
         SPACE 1
         LA    R0,2                     2 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,2(,R2)                POSITION PAST DS1DSORG
         SPACE 1
         LA    R0,1                     1 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,1(,R2)                POSITION PAST DS1RECFM
         SPACE 1
         LA    R0,1                     1 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,1(,R2)                POSITION PAST DS1OPTCD
         SPACE 1
         LH    R0,DS1DSNAM(R2)
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,2(,R2)                SKIP DS1BLKL
         SPACE 1
         LH    R0,DS1DSNAM(R2)
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,2(,R2)                SKIP DS1LRECL
         SPACE 1
         SR    R0,R0
         IC    R0,DS1DSNAM(R2)
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,1(,R2)                SKIP DS1KEYL
         SPACE 1
         LH    R0,DS1DSNAM(R2)
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,2(,R2)                SKIP DS1RKP
         SPACE 1
         LA    R0,1                     1 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,1(,R2)                POSITION PAST DS1DSIND
         SPACE 1
         LA    R0,4                     4 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,4(,R2)                POSITION PAST DS1SCALO
         SPACE 1
         LA    R0,3                     3 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,3(,R2)                POSITION PAST DS1LSTAR
         SPACE 1
         SR    R0,R0
         ICM   R0,B'0011',DS1TRBAL
         BAS   R4,USADEC                CONVERT TO DECIMAL
         BAS   R4,USAFMT
         LA    R2,2(,R2)                SKIP DS1TRBAL
         SPACE 1
         AIF   ('&MVSLEV' GE 'MVS7XX').MVSZOSA                DRK FEB06
         LA    R0,2                     2 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,2(,R2)                POSITION PAST RESERVED
         SPACE 1
         AGO   .MVSZOSB                                       DRK FEB06
.MVSZOSA ANOP                                                 DRK FEB06
         LA    R0,1                     1 INPUT BYTE          DRK FEB06
         BAS   R4,USAHEX                CONVERT TO HEX        DRK FEB06
         BAS   R4,USAFMT                                      DRK FEB06
         LA    R2,1(,R2)                POSITION PAST RESERVEDDRK FEB06
         SPACE 1                                              DRK FEB06
         LA    R0,1                     1 INPUT BYTE          DRK FEB06
         BAS   R4,USAHEX                CONVERT TO HEX        DRK FEB06
         BAS   R4,USAFMT                                      DRK FEB06
         LA    R2,1(,R2)                POSITION PAST DS1TTTHIDRK FEB06
.MVSZOSB ANOP                                                 DRK FEB06
         SPACE 1                                              DRK FEB06
         LA    R1,DS1DSNAM(R2)          POINT TO DATA
         LA    R15,INSERT#1+15          START OF OUTPUT DATA
         LA    R0,10                    10 INPUT BYTES
         BAS   R4,USAHEX10              CONVERT TO HEX
         MVC   INSERT#1+13(4),INSERT#1+15            FIRST FOUR
         MVI   INSERT#1+13+4,C'.'                    POINT
         MVC   INSERT#1+13+4+1(8),INSERT#1+13+4+1+1  NEXT EIGHT
         MVI   INSERT#1+13+4+1+8,C'.'                POINT
         BAS   R4,USAFMT
         LA    R2,10(,R2)               POSITION PAST DS1EXT1
         SPACE 1
         LA    R1,DS1DSNAM(R2)          POINT TO DATA
         LA    R15,INSERT#1+15          START OF OUTPUT DATA
         LA    R0,10                    10 INPUT BYTES
         BAS   R4,USAHEX10              CONVERT TO HEX
         MVC   INSERT#1+13(4),INSERT#1+15            FIRST FOUR
         MVI   INSERT#1+13+4,C'.'                    POINT
         MVC   INSERT#1+13+4+1(8),INSERT#1+13+4+1+1  NEXT EIGHT
         MVI   INSERT#1+13+4+1+8,C'.'                POINT
         BAS   R4,USAFMT
         LA    R2,10(,R2)               POSITION PAST DS1EXT2
         SPACE 1
         LA    R1,DS1DSNAM(R2)          POINT TO DATA
         LA    R15,INSERT#1+15          START OF OUTPUT DATA
         LA    R0,10                    10 INPUT BYTES
         BAS   R4,USAHEX10              CONVERT TO HEX
         MVC   INSERT#1+13(4),INSERT#1+15            FIRST FOUR
         MVI   INSERT#1+13+4,C'.'                    POINT
         MVC   INSERT#1+13+4+1(8),INSERT#1+13+4+1+1  NEXT EIGHT
         MVI   INSERT#1+13+4+1+8,C'.'                POINT
         BAS   R4,USAFMT
         LA    R2,10(,R2)               POSITION PAST DS1EXT3
         SPACE 1
         LA    R0,5                     5 INPUT BYTES
         BAS   R4,USAHEX                CONVERT TO HEX
         BAS   R4,USAFMT
         LA    R2,5(,R2)                POSITION PAST DS1EXT3
         M$ERRST MSGBLANK
         B     NEWCMD
         SPACE 1
USADEC   C     R0,USADEC99
         BH    USADEC10
         CVD   R0,DOUBLE
         MVC   INSERT#1+100+8(8),BLANK128
         MVC   INSERT#1+100(8),USA2020
         ED    INSERT#1+100(7),DOUBLE+5
         LA    R1,INSERT#1+100-1
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1+13(8),0(R1)
         BR    R4
USADEC10 LR    R1,R0
         LA    R1,1023(,R1)
         SRL   R1,10                    THE NEXT HIGHER K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#1+100+8(8),BLANK128
         MVC   INSERT#1+100(7),USA2020
         MVI   INSERT#1+100+7,C'K'
         ED    INSERT#1+100(7),DOUBLE+5
         LA    R1,INSERT#1+100-1
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1+13(8),0(R1)
         BR    R4
USADEC99 DC    F'99999'
         SPACE 3
USAHEX   LA    R1,DS1DSNAM(R2)          POINT TO DATA
         LA    R15,INSERT#1+13          START OF OUTPUT DATA
         SPACE 1
USAHEX10 UNPK  0(3,R15),0(2,R1)         UNPACK TO HEXADECIMAL
         TR    0(2,R15),TRTABLE         CONVERT TO PRINTABLE
         MVI   2(R15),X'40'             BLANK GARBAGE BYTE
         LA    R15,2(,R15)              NEXT OUTPUT DIGIT
         LA    R1,1(,R1)                NEXT INPUT DIGIT
         BCT   R0,USAHEX10              FORMAT ALL INPUT DIGITS
         BR    R4
         SPACE 3
USAFMT   STC   R2,DOUBLE                SAVE OFFSET IN STORAGE
         UNPK  INSERT#1(3),DOUBLE(2)    UNPACK TO HEXADECIMAL
         TR    INSERT#1(2),TRTABLE      CONVERT TO PRINTABLE OFFSET
         MVI   INSERT#1+2,X'40'         CLEAR GARBAGE BYTE
         SPACE 1
USAFMT10 TR    INSERT#1(70),TRLINE      CONVERT TO PRINTABLE CHARACTERS
         M$MSG L186$1                   OUTPUT
         SPACE 1
USAFMT20 MVC   INSERT#1(100),BLANK128   BLANK THE OUTPUT AREA
         MVC   INSERT#1+04(08),0(R3)    ADD THE PDS... NAME
         MVC   INSERT#1+26(44),8(R3)    ADD THE DESCRIPTION
         LR    R6,R3                    THIS TEXT
         LA    R3,52(,R3)               NEXT TEXT
         BR    R4
         SPACE 3
         B     NEWCMD
*                                      .
TOPLINE  DC    CL32'00000000000000001111111111111111'
         DC    CL32'22222222222222223333333333333333'
         DC    CL32'44444444444444445555555555555555'
         DC    CL32'66666666666666667777777777777777'
         DC    CL32'88888888888888889999999999999999'
         DC    CL32'AAAAAAAAAAAAAAAABBBBBBBBBBBBBBBB'
         DC    CL32'CCCCCCCCCCCCCCCCDDDDDDDDDDDDDDDD'
         DC    CL32'EEEEEEEEEEEEEEEEFFFFFFFFFFFFFFFF'
*                                      .
BOTLINE  DC    16CL16'0123456789ABCDEF'
*                                      .
USA2020  DC    X'4020206B2021204B'      EDIT MASK
MSG185T  DC    CL70'FORMAT 1 DSCB: '
*                                      .
MSG185H1 DC    CL70'0   4   8   C  10  14  18  1C  20  24  28  2C  30  X
               34  38  3C  40  44 '
MSG185H2 DC    CL70' 48  4C  50  54  58  5C  60  64  68  6C  70  74  78X
                 7C  80  84  88   '
MSG185H  DC    CL40'LOC NAME     VALUE        DESCRIPTION'
MSG185U  DC    CL40'--- ----     -----        -----------'
USALCCCC DC    X'C0C0C0C0C0C0C0C0C0C0C0C0C0'
*IRHDR   DC CL52'1234567890123456789012345678901234567890123456789012'
USAHDR   DC CL52'DS1DSNAM                                            '
         DC CL52'DS1FMTIDFORMAT IDENTIFIER                           '
         DC CL52'DS1DSSN DATA SET SERIAL NUMBER                      '
         DC CL52'DS1VOLSQVOLUME SEQUENCE NUMBER                      '
         DC CL52'DS1CREDTCREATION DATE                               '
         DC CL52'DS1EXPDTEXPIRATION DATE                             '
         DC CL52'DS1NOEPVNUMBER OF EXTENTS ON VOLUME                 '
         DC CL52'DS1NOBDBNUMBER OF BYTES USED IN LAST DIRECTORY BLOCK'
*        DC CL52'        RESERVED (ONE BYTE)                         '
         DC CL52'DS1FLAG1FLAG 1                                      '
         DC CL52'DS1SYSCDSYSTEM CODE                                 '
         DC CL52'DS1REFD DATE LAST REFERENCED                        '
*        DC CL52'        RESERVED (FOUR BYTES)                       '
         DC CL52'DS1SMSFGSYSTEM MANAGED STORAGE INDICATORS           '
         DC CL52'DS1SCEXTSECONDARY SPACE EXTENSION                   '
         DC CL52'DS1DSORGDATA SET ORGANIZATION                       '
         DC CL52'DS1RECFMRECORD FORMAT                               '
         DC CL52'DS1OPTCDOPTION CODE                                 '
         DC CL52'DS1BLKL BLOCK LENGTH                                '
         DC CL52'DS1LRECLLOGICAL RECORD LENGTH                       '
         DC CL52'DS1KEYL KEY LENGTH                                  '
         DC CL52'DS1RKP  RELATIVE KEY POSITION                       '
         DC CL52'DS1DSINDDATA SET INDICATORS                         '
         DC CL52'DS1SCALOSECONDARY ALLOCATION                        '
         DC CL52'DS1LSTARTTR OF LAST USED TRACK AND BLOCK ON TRACK   '
         DC CL52'DS1TRBALBYTES REMAINING ON LAST TRACK USED          '
         AIF   ('&MVSLEV' GE 'MVS7XX').MVSZOSC                DRK FEB06
         DC CL52'        RESERVED (TWO BYTES)                        '
         AGO   .MVSZOSD                                       DRK FEB06
.MVSZOSC ANOP                                                 DRK FEB06
         DC CL52'        RESERVED (ONE BYTE)                         '
         DC CL52'DS1TTTHIHIGH ORDER BYTE OF TRACK NUMBER IN DS1LSTAR '
.MVSZOSD ANOP                                                 DRK FEB06
         DC CL52'DS1EXT1            FIRST EXTENT DESCRIPTION         '
         DC CL52'DS1EXT2            SECOND EXTENT DESCRIPTION        '
         DC CL52'DS1EXT3            THIRD EXTENT DESCRIPTION         '
         DC CL52'DS1PTRDSCCHHR OF ANY ASSOCIATED FORMAT 2 OR 3 DSCB  '
         COPY  @FAMS                                          DRK SEP10
