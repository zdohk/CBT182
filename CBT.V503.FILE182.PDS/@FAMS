         TITLE 'P D S  --  FAMS INTERFACE                    09/13/10'
***********************************************************************
***      FAMS  SUBROUTINE                                           ***
***********************************************************************
*
         SPACE 1
FAMS     DS    0H                                             DRK SEP10
*--------------------------------------------------------------------*
*        GET FAMS PARM AREA AND ZERO
*--------------------------------------------------------------------*
         USING *,R4
         L     R0,=A(FAMSLEN)         LENGTH OF PARM AREA
         ICM   R0,B'1000',=AL1(28)    SUBPOOL 28
         GETMAIN R,LV=(0)             GETMAIN FAMS PARM AREA
         ST    R1,FAMSADDR            FAMS PARM AREA
         LR    R14,R1                 COPY PARM AREA ADDRESS
         L     R15,=A(FAMSLEN)        LENGTH OF PARM AREA
         XR    R0,R0                  CLEAR
         XR    R1,R1                  CLEAR
         MVCL  R14,R0                 INIT TO ZEROS
*--------------------------------------------------------------------*
*        INITIALIZE FAMS PARM AREA
*--------------------------------------------------------------------*
         L     R5,FAMSADDR            PT @ FAMS PARM AREA
         USING IGWFAMS,R5             MAP FAMS PARM AREA
         LA    R0,HARPN               HIGHEST ALLOC REL PAGE NUM
         ST    R0,IGWPTP1+8
         LA    R0,PAGSUSED            PAGES USED
         ST    R0,IGWPTP2+8
         LA    R0,MEMCOUNT            MEMBER COUNT
         ST    R0,IGWPTP3+8
         MVC   IGWFMH(FAMSHL),FAMSH   INITIALIZE FAMS HEADER
         LA    R0,FAMSDSNL            PT @ DSNAME LENGTH
         ST    R0,IGWFMH1
         MVC   IGWFMH2(4),=X'80000000'
         MVC   IGWFMH3(4),=F'2'       NUMBER OF PARAMETERS  -MEMCOUNT
*        MVC   IGWFMH3(4),=F'3'       NUMBER OF PARAMETERS  +MEMCOUNT
         LA    R0,FAMSLIST            PT @ LIST
         ST    R0,IGWFMH3+4
         SPACE 1
         MVC   IGWFMO(FAMSOL),FAMSO   INITIALIZE FAMS OUTPUT
         MVC   IGWPARM(FAMSPL),FAMSP  INITIALIZE FAMS OPTIONS
         MVC   FAMSDSN(44),DSNAME     DSNAME
         LA    R0,IGWPTP1
         ST    R0,FAMSLIST
         LA    R0,IGWPTP2
         ST    R0,FAMSLIST+4
         LA    R0,IGWPTP3
         ST    R0,FAMSLIST+8
         SPACE 1
         L     R15,CVTPTR(,R0)        LOAD CVT POINTER
         L     R15,CVTDFA-CVT(,R15)   DATA FACILITIES AREA
         CLC   2(2,R15),=X'3200'
         BL    FAMSEXIT
         L     R15,DFADFVAD-DFA(,R15)  DFP VECTOR TABLE
         L     R15,DFVTFMS-DFVT(,R15)  FMS MAIN ENTRY PT
         LTR   R15,R15
         BNZ   CALLFAMS
         B     FAMSEXIT
CALLFAMS DS    0H
         LA    R1,PARMLIST
         LA    R14,IGWFMH
         ST    R14,0(,R1)
         LA    R14,IGWFMO
         ST    R14,4(,R1)
         BASSM R14,R15                IGWFAMS
         LTR   R15,R15                FAMS SUCCESSFUL?
         BNZ   FAMSEXIT               NO
         L     R2,IGWFMO+X'60'        HARPN
         L     R1,IGWFMO+X'64'        PAGSUSED
         DROP  R5
         SPACE 1
         CVD   R1,DOUBLE
         MVC   WORKTBL(15),FAMSPIC
         ED    WORKTBL(15),DOUBLE+2      PAGSUSED
         MH    R1,=H'100'             * 100 FOR PERCENT
         SR    R0,R0                  CLEAR EVEN FOR DIVIDE
         DR    R0,R2                  PERCENT PAGES USED
         CVD   R1,DOUBLE              PERCENT UTILIZATION
         MVC   WORKTBL+15(15),FAMSPIC
         ED    WORKTBL+15(15),DOUBLE+2   PCTUSED
         MVI   MTLEN,39
         MVC   INSERT#1(39),MSG184H3
         M$MSG L184$1
         MVC   INSERT#1(39),BLANK128
         MVC   INSERT#1+12(13),WORKTBL+2
         MVC   INSERT#1+26(13),WORKTBL+17
         M$MSG L184$1
         SPACE 1
FAMSEXIT DS    0H
         L     R1,FAMSADDR            FAMS PARM AREA
         L     R0,=A(FAMSLEN)         LENGTH OF PARM AREA
         ICM   R0,B'1000',=AL1(28)    SUBPOOL 28
         FREEMAIN R,LV=(0),A=(1)      FREEMAIN FAMS PARM AREA
         BR    R3                     RETURN
*-------------------------------------------------------------------*
* CONSTANTS AND DSECTS
*-------------------------------------------------------------------*
*                   1 2 3 4 5 6 7 8 9 0 1 2 3 4 5
*                     1 2 , 3 4 5 , 6 7 8 , 9 0 1
FAMSPIC  DC    XL15'4020206B2020206B2020206B202120'
MSG184H3 DC    CL39'PDSE SPACE:    USED PAGES     %UTILIZED'
*                               1234567890123 1234567890123
         DS    0D
FAMSH    DC    CL8'IGWFMH'
         DC    F'136'
         DC    X'01000000'  VERSION
         DC    X'00010068'  GETATTR
         DC    X'00000400'
FAMSHL   EQU   *-FAMSH
         DS    0D
FAMSO    DC    CL8'IGWFMO'
         DC    F'1104'
         DC    X'01000000'  VERSION
FAMSOL   EQU   *-FAMSO
         DS    0D
FAMSP    DC    CL8'HARPN'
         DC    CL8'PAGSUSED'
         DC    CL8'MEMCOUNT'
         DC    H'44'
FAMSPL   EQU   *-FAMSP
         DROP  R4
         TITLE 'P D S  --  FILE ATTRIBUTE MANAGEMENT SERVICES PARMS  '
IGWFAMS  DSECT
         DS    0D
IGWFMP   DC    72X'00'
         ORG   IGWFMP
IGWPTP1  DC    2F'0'
         DC    A(HARPN)
         DC    3F'0'
IGWPTP2  DC    2F'0'
         DC    A(PAGSUSED)
         DC    3F'0'
IGWPTP3  DC    2F'0'
         DC    A(MEMCOUNT)
         DC    3F'0'
         SPACE 1
         DS    0D
IGWFMH   DC    136X'00'
         ORG   IGWFMH
         DC    CL8'IGWFMH'
         DC    F'136'
         DC    X'01000000'
         DC    X'00010068'
         DC    X'00000400'
IGWFMH1  DC    A(FAMSDSNL)
         ORG   IGWFMH+60
IGWFMH2  DC    X'80000000'
         ORG   IGWFMH+116
IGWFMH3  DC    F'3'
         DC    A(FAMSLIST)
         ORG   ,
         SPACE 1
         DS    0D
IGWFMO   DC    1104X'00'
         ORG   IGWFMO
         DC    CL8'IGWFMO'
         DC    F'1104'
         DC    X'01000000'
         ORG   ,
         DS    0D
IGWPARM  DC    82X'00'
         ORG   IGWPARM
HARPN    DC    CL8'HARPN'
PAGSUSED DC    CL8'PAGSUSED'
MEMCOUNT DC    CL8'MEMCOUNT'
FAMSDSNL DC    H'44'
FAMSDSN  DC    CL44' '
FAMSLIST DC    A(IGWPTP1)
         DC    A(IGWPTP2)
         DC    A(IGWPTP3)
         ORG   ,
FAMSLEN  EQU   *-IGWFAMS
*------  END   IGWFAMS
         SPACE 5
         TITLE 'P D S  --  DATA FACILITIES AREA                      '
         IHADFA
         SPACE 5
         TITLE 'P D S  --  DATA FACILITIES VECTOR TABLE              '
         IHADFVT
         SPACE 5
